function SettingsManager() constructor {
    master_volume = 1.0;
    music_volume = 1.0;
    sfx_volume = 1.0;
    fullscreen = true;
    window_rect = {
        x: 0,
        y: 0,
        w: 1600,
        h: 900,
    };
    autosave = true;
    username = "";
    language = LANG_EN;

    static load = function() {
        ini_open("saves.ini");
        master_volume = ini_read_real("Settings", "master_volume", 1);
        music_volume = ini_read_real("Settings", "music_volume", 1);
        sfx_volume = ini_read_real("Settings", "effect_volume", 1);
        fullscreen = ini_read_real("Settings", "fullscreen", 1);
        autosave = ini_read_real("Settings", "autosave", true);
        username = ini_read_string("Settings", "username", "");
        language = ini_read_string("Settings", "language", LANG_EN);

        // Guard against a stale/invalid language saved in saves.ini that is no
        // longer shipped, so the language selector always starts from a valid index.
        if (array_get_index(global.available_languages, language) == -1) {
            language = LANG_EN;
        }

        var rect_str = ini_read_string("Settings", "window_data", "0|0|1600|900|");
        var parts = string_split(rect_str, "|");
        if (array_length(parts) >= 4) {
            window_rect.x = real(parts[0]);
            window_rect.y = real(parts[1]);
            window_rect.w = real(parts[2]);
            window_rect.h = real(parts[3]);
        }
        ini_close();
    };

    static save = function() {
        ini_open("saves.ini");
        ini_write_real("Settings", "master_volume", master_volume);
        ini_write_real("Settings", "effect_volume", sfx_volume);
        ini_write_real("Settings", "music_volume", music_volume);
        ini_write_real("Settings", "fullscreen", fullscreen);
        ini_write_real("Settings", "autosave", autosave);
        ini_write_string("Settings", "username", username);
        ini_write_string("Settings", "language", language);
        var _window_data = $"{window_rect.x}|{window_rect.y}|{window_rect.w}|{window_rect.h}|";
        ini_write_string("Settings", "window_data", _window_data);
        ini_close();
    };

    static apply_video = function() {
        if (fullscreen) {
            window_set_fullscreen(true);
        } else {
            window_set_fullscreen(false);
            window_set_size(window_rect.w, window_rect.h);
            window_set_position(window_rect.x, window_rect.y);
        }
        sync_ui();
    };

    static apply_audio = function() {
        audio_master_gain(master_volume);

        global.audio_manager.set_music_volume(music_volume);
        global.audio_manager.set_sfx_volume(sfx_volume);
    };

    static apply_language = function() {
        global.language = language;
        global.localization_manager.load_language(language);
        global.localization_manager.refresh_locale_globals();
    };

    static sync_ui = function() {
        var _w = window_get_width();
        var _h = window_get_height();

        if (_w > 0 && _h > 0) {
            display_set_gui_size(1600, 900);
            surface_resize(application_surface, 1600, 900);
        }
    };
}

function approach(current, target, amount) {
    if (current < target) {
        return min(current + amount, target);
    }
    return max(current - amount, target);
}

function start_room_transition(_target_room) {
    if (instance_exists(obj_main_menu_buttons)) {
        obj_main_menu_buttons.target_room = _target_room;
        obj_main_menu_buttons.fade_target = 1;
    } else {
        room_goto(_target_room);
    }
}
