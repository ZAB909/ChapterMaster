function relationship_hostility_matrix(faction) {
    var _rela = "neutral";
    var _disp = disposition[faction];
    with (obj_controller) {
        if (_disp >= 60) {
            _rela = "friendly";
        }
        if ((_disp < 60) && (_disp >= 20)) {
            _rela = "neutral";
        }
        if (_disp < 20) {
            _rela = "hostile";
        }
        if (diplomacy == 6) {
            if (_disp >= 60) {
                _rela = "friendly";
            }
            if ((_disp < 60) && (_disp >= 0)) {
                _rela = "neutral";
            }
            if (_disp < 0) {
                _rela = "hostile";
            }
        }

        if (diplomacy == 8) {
            if (_disp >= 40) {
                _rela = "friendly";
            }
            if ((_disp < 40) && (_disp >= -15)) {
                _rela = "neutral";
            }
            if (_disp < -15) {
                _rela = "hostile";
            }
        }
    }
    return _rela;
}

function alter_disposition(faction, alter_value, return_string = false) {
    chap_data = obj_ini.chapter_data;

    alter_value = chap_data.calc_final_disp_value(faction, alter_value);

    obj_controller.disposition[faction] += alter_value;

    if (return_string){
        return $"{global.faction_names[faction]} : {string_plus_minus(alter_value)}{alter_value}";
    }
}

function alter_dispositions(alterations, return_strings) {
    var _string;
    var _strings = [];
    for (var i = 0; i < array_length(alterations); i++) {
        _string = alter_disposition(alterations[i][0], alterations[i][1], return_strings);
        if (return_strings){
            array_push(_strings, _string);
        }
    }

    if (return_strings){
        return _strings;
    }
}

function clear_diplo_choices() {
    obj_controller.diplo_option = [];
}

function valid_diplomacy_options() {
    var _valid = false;
    var _options_count = array_length(obj_controller.diplo_option);

    if (_options_count == 0) {
        return _valid;
    }

    for (var i = _options_count - 1; i >= 0; i--) {
        var _opt = obj_controller.diplo_option[i];
        LOGGER.debug(_opt);
        if (struct_exists(_opt, "option_text") && _opt.option_text != "") {
            _valid = true;
        } else {
            array_delete(obj_controller.diplo_option, i, 1);
        }
    }

    return _valid;
}

function add_diplomacy_option(option = {}) {
    if (!struct_exists(option, "goto")) {
        option.goto = "";
    }
    if (!struct_exists(option, "key")) {
        option.key = option.option_text;
    }
    var _button = new UnitButtonObject(option);
    _button.style = "pixel";
    _button.label = option.option_text;
    array_push(obj_controller.diplo_option, _button);
}

/// @desc Opens the trade screen for the current diplomacy faction.
/// @returns {undefined}
function open_trade_screen() {
    with (obj_controller) {
        trading = 1;
        scr_dialogue("open_trade");
        cooldown = 8;
        click2 = 1;
        trade_attempt = new TradeAttempt(diplomacy);
    }
}

/// @desc Leaves the artifact negotiation and cleans up the associated objects.
/// @returns {undefined}
function leave_artifact_negotiation() {
    with (obj_ground_mission) {
        instance_destroy();
    }
}

function basic_diplomacy_screen() {
    var xx = camera_get_view_x(view_camera[0]);
    var yy = camera_get_view_y(view_camera[0]);
    if (trading == 0 && valid_diplomacy_options()) {
        if (!force_goodbye) {
            draw_set_halign(fa_center);

            var opts = array_length(diplo_option);
            if (opts == 4) {
                yy -= 30;
            }
            if (opts == 2) {
                yy += 30;
            }
            if (opts == 1) {
                yy += 60;
            }

            option_selections = [];
            var diplo_pressed = -1;
            for (var slot = 0; slot < opts; slot++) {
                var _opt = diplo_option[slot];

                _opt.update({x1: xx + 354, y1: yy + 694});

                if (_opt.draw()) {
                    diplo_pressed = slot;
                }

                yy += 30;
            }
            if (diplo_pressed > -1) {
                evaluate_chosen_diplomacy_option(diplo_pressed);
            }
        }
        if ((menu == eMENU.DIPLOMACY) && (diplomacy == 10.1)) {
            scr_emmisary_diplomacy_routes();
        }
    }
}

function draw_character_diplomacy() {
    var _diplo_unit = character_diplomacy;
    if (_diplo_unit.allegiance == global.chapter_name) {
        var _specific_splash = 0;
        _diplomacy_faction_name = _diplo_unit.name_role();
        _diplo_unit.IsSpecialist(SPECIALISTS_HEADS);

        var _customs = obj_ini.custom_advisors;
        if (_diplo_unit.IsSpecialist(SPECIALISTS_APOTHECARIES)) {
            _specific_splash = struct_exists(_customs, "apothecary") ? _customs.apothecary : 2;
        } else if (_diplo_unit.IsSpecialist(SPECIALISTS_CHAPLAINS)) {
            _specific_splash = struct_exists(_customs, "chaplain") ? _customs.chaplain : 3;
        } else if (_diplo_unit.IsSpecialist(SPECIALISTS_LIBRARIANS)) {
            _specific_splash = struct_exists(_customs, "librarian") ? _customs.librarian : 4;
        } else if (_diplo_unit.IsSpecialist(SPECIALISTS_TECHS)) {
            _specific_splash = struct_exists(_customs, "forge_master") ? _customs.forge_master : 5;
        }
        scr_image("advisor/splash", _specific_splash, 16, 43, 310, 828);
    }

    var _main_slate = diplo_buttons.main_slate;
    var _meet = diplo_buttons.meet_slate;
    var _cm_slate = diplo_buttons.cm_slate;
    _meet.XX = 0;
    _meet.YY = 520;
    _meet.width = 520;

    _meet.inside_method = function() {
        var _diplo_unit = obj_controller.character_diplomacy;
        if (!variable_instance_exists(obj_controller, "diplo_image")) {
            obj_controller.diplo_image = _diplo_unit.draw_unit_image();
        }
        obj_controller.diplo_image.draw(210, 249, true, 1, 1, 0, CM_GREEN_COLOR, 1);
        _diplo_unit.stat_display(false, {x1: 10, y1: 520, w: 569, h: 303}, true);
        draw_sprite(spr_holo_pad, 0, 210, 520);
    };

    _meet.draw_with_dimensions();

    _main_slate.XX = _meet.XX + _meet.width;
    _main_slate.YY = 175;
    _main_slate.draw_with_dimensions();
    draw_diplomacy_diplo_text();
    draw_set_halign(fa_center);
    draw_text_transformed(622, 104, $"{_diplo_unit.name_role()}", 0.6, 0.6, 0);
    draw_set_halign(fa_left);

    _cm_slate.XX = _main_slate.XX + _main_slate.width;
    _cm_slate.YY = 520;
    _cm_slate.inside_method = function() {
        var _master = fetch_unit([0, 0]);
        if (!variable_instance_exists(obj_controller, "master_image")) {
            obj_controller.master_image = _master.draw_unit_image();
        }
        obj_controller.master_image.draw(1308, 249, true, 1, 1, 0, CM_GREEN_COLOR, 1);
        _master.stat_display(false, {x1: 1108, y1: 520, w: 569, h: 303}, true);
        draw_sprite(spr_holo_pad, 0, 1308, 520);
    };
    _cm_slate.draw_with_dimensions();

    basic_diplomacy_screen();
}

function evaluate_chosen_diplomacy_option(diplo_pressed) {
    var _opt = diplo_option[diplo_pressed];

    var _pressed_option = _opt.key;
    if (struct_exists(_opt, "choice_func")) {
        if (is_callable(_opt.choice_func)) {
            script_execute(_opt.choice_func);
        }
    }
    if (_opt.goto != "") {
        scr_dialogue(_opt.goto);
    }

    if (struct_exists(_opt, "is_exit") && _opt.is_exit) {
        exit_diplomacy_dialogue();
    }
}

function scr_diplomacy_hit(selection, new_path = undefined, complex_path = undefined) {
    if (array_length(option_selections) > selection) {
        if (point_and_click(option_selections[selection])) {
            if (!is_method(complex_path)) {
                diplomacy_pathway = new_path;
                scr_dialogue(diplomacy_pathway);
            } else {
                complex_path();
            }
        }
    } else {
        return false;
    }
}

// ** Diplomacy Chaos talks **
function scr_emmisary_diplomacy_routes() {
    if (cooldown > 0) {
        exit;
    }
    if (diplomacy_pathway == "intro") {
        //TODO replace with methods more in line with rest of code base but this helps find bugs for now
        scr_diplomacy_hit(0, "gift");
        scr_diplomacy_hit(1, "daemon_scorn");
        scr_diplomacy_hit(2, "daemon_scorn");
    } else if (diplomacy_pathway == "gift") {
        scr_diplomacy_hit(0, "Khorne_path");
        scr_diplomacy_hit(1, "Nurgle_path");
        scr_diplomacy_hit(2, "Tzeentch_path");
        scr_diplomacy_hit(3, "Slaanesh_path");
    } else if (diplomacy_pathway == "Khorne_path") {
        scr_diplomacy_hit(0,, function() {
            //TODO central get cm choice_func
            cooldown = 8000;
            diplomacy_pathway = "sacrifice_lib";
            //grab a random librarian
            var lib = scr_random_marine(SPECIALISTS_LIBRARIANS, 0);
            if (lib != "none") {
                var chapter_master = fetch_unit([0, 1]);
                var dead_lib = fetch_unit([lib[0], lib[1]]);
                pop_up = instance_create(0, 0, obj_popup);
                pop_up.title = "Skull for the Skull Throne";
                pop_up.text = $"You summon {dead_lib.name_role()} to your personal chambers. Darting from the shadows you deftly strike his head from his shoulders. With the flesh removed from his skull you place the skull upon a hastily erected shrine.";
                pop_up.type = 98;
                pop_up.image = "chaos";
                dead_lib.kill();
                chapter_master.add_trait("blood_for_blood");
                chapter_master.edit_corruption(20);
            } else {
                diplomacy_pathway = "daemon_scorn";
            }
            scr_dialogue(diplomacy_pathway);
            force_goodbye = 1;
        });
        scr_diplomacy_hit(1,, function() {
            cooldown = 8000;
            diplomacy_pathway = "sacrifice_champ";
            var champ = scr_random_marine(obj_ini.player_role_data[eROLE.CHAMPION].role, 0);
            if (champ != "none") {
                var chapter_master = fetch_unit([0, 1]);
                chapter_master.add_trait("blood_for_blood");
                chapter_master.edit_corruption(20);
                var dead_champ = fetch_unit([champ[0], champ[1]]);
                //TODO make this into a real dual with consequences
                pop_up = instance_create(0, 0, obj_popup);
                pop_up.title = "Skull for the Skull Throne";
                pop_up.text = $"You summon {dead_champ.name_role()} to your personal chambers. Darting from the shadows towards {dead_champ.name()} who is a cunning warrior and reacts with precision to your attack, however eventually you prevail and strike him down. With the flesh removed from his skull you place it upon a hastily erected shrine.";
                pop_up.type = 98;
                pop_up.image = "chaos";
                dead_champ.kill();
            } else {
                diplomacy_pathway = "daemon_scorn";
            }
            scr_dialogue(diplomacy_pathway);
            force_goodbye = 1;
        });
        scr_diplomacy_hit(2,, function() {
            cooldown = 8000;
            diplomacy_pathway = "sacrifice_squad";
            var kill_squad, squad_found = false;
            var _squad_ids = get_squad_ids();
            for (var i = 0; i < array_length(_squad_ids); i++) {
                kill_squad = fetch_squad(_squad_ids[i]);
                if (kill_squad.type == "tactical_squad" && array_length(kill_squad.members) > 4) {
                    var chapter_master = fetch_unit([0, 1]);
                    chapter_master.add_trait("blood_for_blood");
                    chapter_master.edit_corruption(20);
                    kill_squad.kill_members();
                    with (obj_ini) {
                        scr_company_order(kill_squad.base_company);
                    }
                    squad_found = true;
                    break;
                }
            }
            if (!squad_found) {
                diplomacy_pathway = "daemon_scorn";
            }
            scr_dialogue(diplomacy_pathway);
            force_goodbye = 1;
        });
        scr_diplomacy_hit(3, "daemon_scorn");
    } else if (diplomacy_pathway == "Slaanesh_path") {
        scr_diplomacy_hit(0, "Slaanesh_arti");
        scr_diplomacy_hit(1, "daemon_scorn");
    } else if (diplomacy_pathway == "Nurgle_path") {
        scr_diplomacy_hit(0, "nurgle_gift");
        scr_diplomacy_hit(1, "daemon_scorn");
    } else if (diplomacy_pathway == "Tzeentch_path") {
        scr_diplomacy_hit(0, "Tzeentch_plan");
        scr_diplomacy_hit(1, "daemon_scorn");
    }
}
