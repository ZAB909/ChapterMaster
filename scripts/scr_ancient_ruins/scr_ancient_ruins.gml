/// @self Struct.NewPlanetFeature
function scr_ancient_ruins_setup() {
    var ruin_data = choose(["tiny", 5], ["small", 15], ["medium", 55], ["large", 110], ["sprawling", 0]);
    ruins_size = ruin_data[0];
    man_size_limit = ruin_data[1];
    recoverable_gene_seed = 0;
    recoverables = [];
    failed_exploration = 0;
    unrecovered_items = false;
    ruins_race = 0;
    f_type = eP_FEATURES.ANCIENT_RUINS;
    exploration_complete = false;
    planet_display = $"{ruins_size} Unexplored Ancient Ruins";
    completion_level = 0;
    player_hidden = 1;
}

/// @self Struct.PlanetData
function scr_ruins_suprise_attack_player() {
    try {
        instance_deactivate_all_safe();
        instance_activate_object(obj_star_select);
        instance_activate_object(obj_star);
        instance_activate_object(obj_ground_mission);
        var _star = find_star_by_name(obj_ground_mission.loc);
        var _planet = obj_ground_mission.num;
        var _units = obj_ground_mission.display_unit;

        instance_create(0, 0, obj_ncombat);

        obj_ncombat.man_size_limit = man_size_limit;

        _roster = new Roster();
        with (_roster) {
            roster_location = obj_ground_mission.loc;
            roster_planet = _planet;
            selected_units = _units;
            if (array_length(selected_units)) {
                setup_battle_formations();
                add_to_battle();
            } else {
                instance_destroy(obj_ncombat);
                instance_destroy(obj_pnunit);
                instance_destroy(obj_enunit);
                instance_activate_all();
                scr_ruins_reward(_star, _planet, self);
            }
        }

        obj_ncombat.battle_object = _star;
        obj_ncombat.battle_loc = _star.name;
        instance_deactivate_object(obj_star);
        obj_ncombat.battle_id = _planet;
        obj_ncombat.battle_special = "ruins";
        if (obj_ground_mission.ruins_race == 6) {
            obj_ncombat.battle_special = "ruins_eldar";
        }
        obj_ncombat.dropping = 0;
        obj_ncombat.attacking = 0;
        obj_ncombat.enemy = obj_ground_mission.ruins_battle;
        obj_ncombat.threat = obj_ground_mission.battle_threat;
        obj_ncombat.formation_set = 1;
        instance_destroy(obj_popup);
        instance_destroy(obj_star_select);
    } catch (_exception) {
        ERROR_HANDLER.handle_exception(_exception);
        instance_activate_all();
        instance_destroy(obj_popup);
        instance_destroy(obj_star_select);
        instance_destroy(obj_ground_mission);
        instance_destroy(obj_ncombat);
    }
}

//spawn point for starship
/// @self Struct.NewPlanetFeature
function scr_ruins_find_starship() {
    f_type = eP_FEATURES.STARSHIP;
    planet_display = "Ancient Starship";
    funds_spent = 0;
    player_hidden = 0;
    engineer_score = 0;
}

//allows ruins to be entered to retrive fallen marine gear
/// @self Struct.NewPlanetFeature
function scr_ruins_player_forces_defeated() {
    planet_display = "Failed Ruins Expidition";
    completion_level = 1;
    failed_exploration = 1;
    player_hidden = 0;
    exploration_complete = false;
    failiure_turn = obj_controller.turn;
}

//revcover equipment of fallen marines from ruins
/// @self Struct.NewPlanetFeature
function scr_ruins_recover_from_dead() {
    /// @type {Asset.GMObject.obj_popup}
    var pop = instance_create(0, 0, obj_popup);
    var route = random(5);
    pop.image = "ancient_ruins";
    pop.title = "Ancient Ruins: Recovery";
    if (route < 4) {
        var _weapon_text = "";

        var some_recoverable = false;
        //calculate equipment degredation
        var equipment_deg = (obj_controller.turn - failiure_turn) / 120;
        if (equipment_deg < 1 && array_length(recoverables)) {
            var _recovered = new EquipmentTracker();
            var _recover_picks = ceil((1 - equipment_deg) * array_length(recoverables));
            recoverables = array_shuffle(recoverables);
            if (_recover_picks < array_length(recoverables)) {
                array_delete(recoverables, _recover_picks, array_length(recoverables) - _recover_picks);
            }
            _recovered.items = recoverables;
            _recovered.collate_types();
            _weapon_text = _recovered.item_description_string();

            some_recoverable = array_length(recoverables) > 0;
        } else {
            some_recoverable = false;
        }

        if (some_recoverable) {
            pop.text = $"Your strike team locates the site where the previous expedition made their last stand. They airlift whatever equipment and vehicles remain, disposing of anything beyond saving;. {_weapon_text} is repaired and restored to the armamentarium";
        } else {
            pop.text = $"our strike team locates the site where the previous expedition made their last stand. They cannot find any intact equipment, and are forced to burn the derelicts to prevent capture; no equipment is added to the armamentarium";
        }

        //calculate geneseed degredation
        if (obj_controller.turn - failiure_turn > 2) {
            recoverable_gene_seed -= obj_controller.turn - failiure_turn;
        }
        if (recoverable_gene_seed > 0) {
            pop.text += $" The strike team returns with remains, apothecaries report the gene-seed was able to be saved;{recoverable_gene_seed} gene-seed is harvested from the chapter’s fallen. At least their genetic legacy will continue, we will recover from this.";
            obj_controller.gene_seed += recoverable_gene_seed;
        } else {
            pop.text += $"The strike team returns with remains, but apothecaries report the gene-seed is too contaminated to use; no gene-seed is harvested from the chapter’s fallen. Their legacy lives on through their armaments, we will hold onto their memory.";
        }
    } else {
        pop.text = "Your strike team locates the site where the previous expedition made their last stand. They find nothing. Your equipment is gone and bodies nowhere to be found, the entire expedition appears to have vanished without a trace; they return empty handed. Something insidious happened. You must find whoever defiled your brothers, and eliminate them, forever.";
    }
    unrecovered_items = false;
    recoverable_gene_seed = 0;
    recoverables = [];
    planet_display = "Unexplored Ancient Ruins";
}

//mark ruins as fully explored
/// @self Struct.NewPlanetFeature
function scr_ruins_explored() {
    planet_display = "Ancient Ruins";
    exploration_complete = true;
}

//determine what race the ruins once belonged to effect enemies that can be found
/// @self Struct.NewPlanetFeature
function scr_ruins_determine_race() {
    var dice = floor(random(100)) + 1;
    if (dice <= 9) {
        ruins_race = 1;
    }
    if ((dice > 9) && (dice <= 74)) {
        ruins_race = 2;
    }
    if ((dice > 74) && (dice <= 83)) {
        ruins_race = 5;
    }
    if ((dice > 83) && (dice <= 91)) {
        ruins_race = 6;
    }
    if (dice > 91) {
        ruins_race = 10;
    }
}

/// @self Struct.PlanetData
function scr_explore_ruins() {
    try {
        obj_controller.current_planet_feature = self;
        obj_controller.menu = 0;

        /// @type {Asset.GMObject.obj_popup}
        var pip = instance_create(0, 0, obj_popup);
        pip.title = "Ancient Ruins";

        var nu = planet_numeral_name(planet, star);

        /// @type {Asset.GMObject.obj_ground_mission}
        var arti = instance_create(star.x, star.y, obj_ground_mission);
        arti.explore_feature = self;
        arti.num = planet;
        arti.loc = star.name;
        arti.battle_loc = star.name;
        arti.manag = obj_controller.managing;
        arti.obj = star;
        with (arti) {
            setup_planet_mission_group();
        }

        arti.ship_id = obj_controller.ma_lid[1];
        obj_controller.current_planet_feature.battle = arti;

        if (failed_exploration) {
            pip.text = $"The accursed ruins on {nu} where your brothers fell still holds many secrets including the remains of your brothers honour demands you avenge them.";
        } else {
            pip.text = $"Located upon {nu} is a {ruins_size} expanse of ancient ruins, dating back to times long since forgotten.  Locals are superstitious about the place- as a result the ruins are hardly explored.  What they might contain, and any potential threats, are unknown.";
            switch (ruins_size) {
                case "tiny":
                    pip.text += "It's tiny nature means no more than five marines can operate in cohesion without being seperated";
                    break;
                case "small":
                    pip.text += "As a result of it's narrow corridors and tight spaces a squad of any more than 15 would struggle to operate effectivly";
                    break;
                case "medium":
                    pip.text += "Half a standard company (55) could easily operate effectivly in the many wide spaces and caverns";
                    break;
                case "large":
                    pip.text += "A whole company (110) would not be confined in the huge spaces that such a ruin contain";
                    break;
                case "sprawling":
                    pip.text += "The ruins is of an unprecidented size whole legions of old would not feel uncomfortable in such a space";
                    break;
            }
            pip.text += ". What is thy will?";
        }

        pip.add_option(
            [
                {str1: "Explore the ruins.", choice_func: ruins_exploration_main_sequence},
                {
                    str1: "Do nothing.",
                    choice_func: function() {
                        // Nothing
                        scr_toggle_manage();
                        with (obj_ground_mission) {
                            instance_destroy();
                        }
                        instance_destroy();
                        exit;
                    },
                },
                {
                    str1: "Return your marines to the ship.",
                    choice_func: function() {
                        // Return to ship, exit
                        scr_return_ship(obj_ini.ship[obj_ground_mission.ship_id], obj_ground_mission, obj_ground_mission.num);
                        obj_controller.menu = 0;
                        obj_controller.managing = 0;
                        obj_controller.cooldown = 10;
                        with (obj_ground_mission) {
                            instance_destroy();
                        }
                        instance_destroy();
                        exit;
                    },
                },
            ],
        );
        pip.image = "ancient_ruins";
    } catch (_exception) {
        ERROR_HANDLER.handle_exception(_exception);
    }
}

/// @self Asset.GMObject.obj_popup
function ruins_exploration_main_sequence() {
    // Begin
    /// @type {Struct.NewPlanetFeature}
    var _ruins = obj_ground_mission.explore_feature;
    var battle_threat = 0;

    _ruins.determine_race();

    var dice = roll_dice_chapter(1, 100, "high");
    var ruins_battle = dice <= 50;

    if (ruins_battle == 1) {
        dice = roll_dice_chapter(1, 100, "low");

        if (dice >= 0 && dice <= 60) {
            battle_threat = 1;
        } else if (dice > 60 && dice <= 90) {
            battle_threat = 2;
        } else if (dice < 99) {
            battle_threat = 3;
        } else {
            battle_threat = 4;
        }

        switch (_ruins.ruins_race) {
            case eFACTION.PLAYER:
            case eFACTION.IMPERIUM:
            case eFACTION.CHAOS:
                ruins_battle = choose(10, 10, 10, 10, 11, 11, 12);
                break;
            case eFACTION.ECCLESIARCHY:
                ruins_battle = 10;
                break;
            case eFACTION.ELDAR:
                ruins_battle = choose(6, 6, 10, 10, 10, 12);
                break;
            default:
                ruins_battle = choose(6, 10, 12);
                break;
        }

        obj_ground_mission.ruins_race = _ruins.ruins_race;
        obj_ground_mission.ruins_battle = ruins_battle;
        obj_ground_mission.battle_threat = battle_threat;

        reset_popup_options();
        text = "Your marines descended into the ancient ruins, mapping them out as they go.  They quickly determine the ruins were once ";
        switch (_ruins.ruins_race) {
            case eFACTION.PLAYER:
                text += "a Space Marine fortification from earlier times.";
                break;
            case eFACTION.IMPERIUM:
                text += "golden-age Imperial ruins, lost to time.";
                break;
            case eFACTION.ECCLESIARCHY:
                text += "a magnificent temple of the Imperial Cult.";
                break;
            case eFACTION.ELDAR:
                text += "Eldar colonization structures from an unknown time.";
                break;
            case eFACTION.CHAOS:
                text += "golden-age Imperial ruins, since decorated with spikes and bones.";
                break;
        }

        if (_ruins.failed_exploration == 1) {
            text += $"{global.chapter_name} see the scarring in the walls and round impacts where your brothers died to clense this place of it's foul inhabitants";
        }
        text += "  Unfortunantly, it's too late before your Battle Brothers discern the ruins are still inhabited.  Shapes begin to descend upon them from all directions, masked in the shadows.";

        cooldown = 15;
        add_option({
            str1: "To Battle",
            choice_func: function() {
                instance_deactivate_all_safe();
                instance_activate_object(obj_ground_mission);
                instance_activate_object(obj_popup);
                var _explore_feature = obj_ground_mission.explore_feature;
                _explore_feature.suprise_attack();
                instance_destroy(self.id);
                instance_destroy();
            },
        });
        exit;
    } else {
        var obj = obj_ground_mission.obj;
        instance_activate_object(obj_star);
        scr_ruins_reward(find_star_by_name(obj_ground_mission.battle_loc), obj_ground_mission.num, obj_ground_mission.explore_feature);
        instance_destroy();
        exit;
    }
}

/// @self Struct.PlanetData
function scr_check_for_ruins_exploration() {
    var _ruins_list = get_features(eP_FEATURES.ANCIENT_RUINS);
    /// @type {Struct.NewPlanetFeature}
    var _explore_ruins = 0;
    if (array_length(_ruins_list) > 0) {
        for (var _ruin = 0; _ruin < array_length(_ruins_list); _ruin++) {
            /// @type {Struct.PlanetData}
            var _cur_ruins = _ruins_list[_ruin];
            if (_cur_ruins.exploration_complete == false) {
                _explore_ruins = _cur_ruins;
                break;
            } else {
                _explore_ruins = 0;
            }
        }
        if (_explore_ruins != 0) {
            _explore_ruins.star = system;
            _explore_ruins.planet = planet;
            _explore_ruins.explore();
        }
    }
}

/// @self Struct.NewPlanetFeature
function scr_ruins_combat_end() {
    ruins_battle = choose(6, 7, 9, 10, 11, 12);

    /// @type {Asset.GMObject.obj_star}
    var _star = find_star_by_name(obj_ground_mission.battle_loc);
    var planet = obj_ground_mission.num;
    var _battle_threat = obj_ground_mission.battle_threat;
    if (obj_ground_mission.defeat == 0) {
        var dice = roll_dice_chapter(1, 100, "low");

        if (dice < (_battle_threat * 10)) {
            if (ruins_race == eFACTION.ECCLESIARCHY) {
                obj_controller.disposition[5] += 2;

                if (scr_has_adv("Reverent Guardians")) {
                    obj_controller.disposition[5] += 1;
                }
            }

            if (ruins_race < 5) {
                var di = choose(eFACTION.IMPERIUM, eFACTION.INQUISITION);
                switch (di) {
                    case eFACTION.IMPERIUM:
                        obj_controller.disposition[eFACTION.IMPERIUM] += 2;
                        break;
                    case eFACTION.INQUISITION:
                        obj_controller.disposition[eFACTION.INQUISITION] += 1;
                        break;
                }
            } else if (ruins_race == eFACTION.ELDAR) {
                switch (ruins_battle) {
                    case 6:
                        obj_controller.disposition[eFACTION.ELDAR] -= 5;
                        break;
                    case 11:
                        obj_controller.disposition[eFACTION.ELDAR] += 2;
                        break;
                    case 12:
                        obj_controller.disposition[eFACTION.ELDAR] += 4;
                        break;
                }
            }
        }

        scr_ruins_reward(_star, planet, self);
    } else if (obj_ground_mission.defeat == 1) {
        var dice = roll_dice_chapter(1, 100, "low");

        if (dice < (_battle_threat * 10)) {
            if (ruins_race == eFACTION.ECCLESIARCHY) {
                obj_controller.disposition[5] -= 2;
            } else if (ruins_race < 5) {
                var di = choose(eFACTION.IMPERIUM, eFACTION.INQUISITION);
                switch (di) {
                    case eFACTION.IMPERIUM:
                        obj_controller.disposition[eFACTION.IMPERIUM] -= 2;
                        break;
                    case eFACTION.INQUISITION:
                        obj_controller.disposition[eFACTION.INQUISITION] -= 1;
                        break;
                }
            }
        }
        /// @type {Asset.GMObject.obj_popup}
        var pop = instance_create(0, 0, obj_popup);
        switch (ruins_battle) {
            case 10:
                _star.p_traitors[planet] = _battle_threat + 1;
                _star.p_heresy[planet] += 10;
                break;
            case 11:
                _star.p_traitors[planet] = _battle_threat + 1;
                _star.p_heresy[planet] += 25;
                break;
            case 12:
                _star.p_demons[planet] = _battle_threat + 1;
                _star.p_heresy[planet] += 40;
                break;
        }

        pop.title = "Ancient Ruins";
        pop.text = "Your forces within the ancient ruins have been surrounded and destroyed, down to the last man. An immediate expedition must be launched to recover and honour them as well as secure any geneseed or equipment not destroyed";
        switch (ruins_battle) {
            case 10:
                pop.text += "Now that they have been discovered, scans indicate the heretics and mutants are leaving the structures en masse.  ";
                break;
            case 11:
                pop.text += "Now that they have been discovered, scans indicate the chaos space marines are leaving the structures, intent on doing damage.  ";
                break;
            case 12:
                pop.text += "Scans indicate the foul daemons are leaving the structures en masse, intent on doing damage.  ";
                break;
            case 6:
                pop.text += "Now that they have been discovered, the Eldar seem to have vanished without a trace.  Scans reveal nothing.";
        }
        forces_defeated();
        var _equip_lost = obj_ground_mission.post_equipment_lost;

        recoverable_gene_seed = obj_ground_mission.recoverable_gene_seed;

        if (array_length(_equip_lost.items)) {
            _equip_lost.items = array_shuffle(_equip_lost.items);
            array_delete(_equip_lost.items, 0, floor(array_length(_equip_lost.items) / 2));
        }

        if (array_length(_equip_lost.items) > 0) {
            recoverables = _equip_lost.items;
            unrecovered_items = true;
        }
    }
}
