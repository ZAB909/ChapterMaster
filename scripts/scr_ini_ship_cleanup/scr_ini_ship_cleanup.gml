function scr_kill_ship(index) {
    try {
        with (obj_ini) {
            var _units_on_ship = [];
            var _unit;
            for (var co = 0; co <= companies; co++) {
                for (var i = 0; i < array_length(TTRPG[co]); i++) {
                    _unit = fetch_unit([co, i]);
                    if (!is_struct(_unit)) {
                        continue;
                    }
                    if (_unit.ship_location > -1) {
                        if (_unit.ship_location == index) {
                            if (!(irandom(_unit.luck) - 3)) {
                                _unit.kill(false, false);
                            } else {
                                array_push(_units_on_ship, _unit);
                            }
                        } else {
                            if (_unit.ship_location > index) {
                                _unit.ship_location--;
                            }
                        }
                    }
                }
                for (var i = 0; i < array_length(veh_role[co]); i++) {
                    if (veh_lid[co][i] == index) {
                        reset_vehicle_variable_arrays(co, i);
                    } else if (veh_lid[co][i] > index) {
                        veh_lid[co][i]--;
                    }
                }
            }
            var in_warp = ship_location[index] == "Warp";
            var _available_ships = [];
            var _ship_fleet = find_ships_fleet(index);
            var _nearest_star = noone;
            if (!in_warp) {
                _nearest_star = find_star_by_name(ship_location[index]);
            }
            if (_ship_fleet != noone) {
                delete_ship_from_fleet(index, _ship_fleet);
                _available_ships = fleet_full_ship_array(_ship_fleet);
            }
            _units_on_ship = array_shuffle(_units_on_ship);
            for (var i = 0; i < array_length(_available_ships); i++) {
                if (_available_ships[i] == index) {
                    continue;
                }
                var _cur_ship = _available_ships[i];
                var f = 0;
                var _total_units = array_length(_units_on_ship);
                while (ship_carrying[_cur_ship] < ship_capacity[_cur_ship] && f < _total_units && array_length(_units_on_ship) > 0) {
                    f++;
                    if (_units_on_ship[0].get_unit_size() + ship_carrying[_cur_ship] <= ship_capacity[_cur_ship]) {
                        _units_on_ship[0].load_marine(_cur_ship);
                        array_delete(_units_on_ship, 0, 1);
                    }
                }
            }
            var _index = index;
            with (obj_p_fleet) {
                for (var i = 0; i < array_length(escort_num); i++) {
                    if (escort_num[i] > _index) {
                        escort_num[i]--;
                    }
                }
                for (var i = 0; i < array_length(capital_num); i++) {
                    if (capital_num[i] > _index) {
                        capital_num[i]--;
                    }
                }
                for (var i = 0; i < array_length(frigate_num); i++) {
                    if (frigate_num[i] > _index) {
                        frigate_num[i]--;
                    }
                }
            }

            // Artifacts store ship indices (__sid) that shift when a ship is removed.
            // get_ship_id() resolves through the bearer while equipped, and the unit loop
            // above already shifted the bearer's ship_location, so the shift must use
            // the raw stored index (get_stored_ship_id()) instead of the resolved one.
            // Relocate artifacts whose ship is lost; shift the rest down.
            var _art_keys = struct_get_names(artifact_map);
            for (var _i = 0; _i < array_length(_art_keys); _i++) {
                var _arti = artifact_map[$ _art_keys[_i]];
                var _sid = _arti.get_ship_id();
                var _stored_sid = _arti.get_stored_ship_id();
                if ((_sid == index) || (_stored_sid == index)) {
                    _arti.set_sid(-1);
                    if (_nearest_star != noone) {
                        _arti.set_location_name(_nearest_star.name);
                    } else {
                        _arti.set_location_name("");
                    }
                } else if (_stored_sid > index) {
                    _arti.set_sid(_stored_sid - 1);
                }
            }

            array_delete(ship, index, 1);
            array_delete(ship_uid, index, 1);
            array_delete(ship_owner, index, 1);
            array_delete(ship_class, index, 1);
            array_delete(ship_size, index, 1);
            array_delete(ship_leadership, index, 1);
            array_delete(ship_hp, index, 1);
            array_delete(ship_maxhp, index, 1);

            array_delete(ship_location, index, 1);
            array_delete(ship_shields, index, 1);
            array_delete(ship_conditions, index, 1);
            array_delete(ship_speed, index, 1);
            array_delete(ship_turning, index, 1);

            array_delete(ship_front_armour, index, 1);
            array_delete(ship_other_armour, index, 1);
            array_delete(ship_weapons, index, 1);

            array_delete(ship_wep, index, 1);
            array_delete(ship_wep_condition, index, 1);
            array_delete(ship_wep_facing, index, 1);

            array_delete(ship_capacity, index, 1);
            array_delete(ship_carrying, index, 1);
            array_delete(ship_contents, index, 1);
            array_delete(ship_turrets, index, 1);

            if (!in_warp) {
                if (_nearest_star != noone) {
                    while (array_length(_units_on_ship) > 0) {
                        _unit = array_pop(_units_on_ship);
                        if (irandom(100) > 100 - _unit.luck) {
                            _unit.unload(irandom_range(1, _nearest_star.planets), _nearest_star);
                        }
                    }
                }
            }
            for (var i = 0; i < array_length(_units_on_ship); i++) {
                _unit = _units_on_ship[i];
                if (!is_struct(_unit)) {
                    continue;
                }
                // The ship arrays already shrank: invalidate the unit's location so
                // clear_bearer() drops its artifacts at the destroyed ship's location
                // instead of the ship that now occupies the stale index.
                _unit.ship_location = -1;
                if (_nearest_star != noone) {
                    _unit.location_string = _nearest_star.name;
                } else {
                    _unit.location_string = "";
                }
                _unit.kill(false, false);
            }
        }
    } catch (_exception) {
        ERROR_HANDLER.handle_exception(_exception);
    }
}

function scr_ini_ship_cleanup() {
    // If the ship is dead then make it fucking dead man
    with (obj_ini) {
        if (array_length(ship)) {
            for (var i = array_length(ship) - 1; i >= 0; i--) {
                if ((ship[i] != "") && (ship_hp[i] <= 0)) {
                    scr_kill_ship(i);
                }
            }
        }
        sort_all_companies();
    }
}
