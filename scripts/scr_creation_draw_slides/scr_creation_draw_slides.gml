enum eCREATION_SLIDES {
    CHAPTERSELECT = 1,
    CHAPTERTRAITS = 2,
    CHAPTERHOME = 3,
    CHAPTERLIVERY = 4,
    CHAPTERROLES = 5,
    CHAPTERGENE = 6,
    CHAPTERMASTER = 7,
}

/// @self Asset.GMObject.obj_creation
function draw_chapter_select() {
    draw_set_color(CM_GREEN_COLOR);
    draw_set_font(cjk_font(fnt_40k_30b));
    draw_set_halign(fa_center);
    draw_text(800, 80, localize("Select Chapter"));

    draw_set_font(cjk_font(fnt_40k_30b));
    draw_set_halign(fa_left);

    draw_text_transformed(440, founding_y, localize("Founding Chapters"), 0.75, 0.75, 0);
    draw_text_transformed(440, successor_y, localize("Existing Chapters"), 0.75, 0.75, 0);
    draw_text_transformed(440, custom_y, localize("Custom Chapters"), 0.75, 0.75, 0);
    draw_text_transformed(440, other_y, localize("Other"), 0.75, 0.75, 0);

    /// @localvar grid object to keep track of where to draw icon boxes
    var grid = {
        count: 0,
        x1: icon_grid_left_edge,
        y1: founding_y + icon_gap_y,
        w: icon_width,
        h: icon_height,
        x2: 0,
        y2: 0,
        left_edge: icon_grid_left_edge,
        right_edge: icon_grid_right_edge(),
        row_gap: icon_row_gap,
        section_gap: icon_gap_y,
        col_gap: icon_gap_x,
        /// Updates coords to draw a new icon, creating new rows where needed
        new_cell: function() {
            if (count > 0) {
                x1 = x1 + col_gap;
            } else {
                x2 = x1 + w;
                y2 = y1 + h;
            }
            if (x1 > right_edge) {
                x1 = left_edge;
                y1 = y1 + row_gap;
            }
            x2 = x1 + w;
            y2 = y1 + h;
            count += 1;
        },
        /// given a new y coord for a section heading resets cell drawing to start a new grid
        new_section: function(new_y) {
            count = 0;
            x1 = left_edge;
            y1 = new_y + section_gap;
            x2 = x1 + w;
            y2 = y1 + h;
        },
        hover: function() {
            return scr_hit(x1, y1, x2, y2);
        },
        clicked: function() {
            return point_and_click([x1, y1, x2, y2]);
        },
    };

    //* Founding Chapters *//
    var i = 1;
    var tool = 0;
    for (var c = 0; c < array_length(founding_chapters); c++) {
        var chap = founding_chapters[c];
        i = chap.id;

        grid.new_cell();

        draw_sprite(spr_creation_icon, 0, grid.x1, grid.y1);
        draw_sprite_stretched(global.chapter_icons_map[? chap.icon_name], 0, grid.x1, grid.y1, grid.w, grid.h);

        // Hover
        if (grid.hover() && slate4 >= 30) {
            if ((old_highlight != highlight) && (highlight != i) && (goto_slide != 2)) {
                old_highlight = highlight;
                highlighting = 1;
            }
            if (goto_slide != 2) {
                highlight = i;
                tool = 1;
            }
            // Highlight white on hover
            draw_rectangle_color_simple(grid.x1, grid.y1, grid.x2, grid.y2, 0, c_white, 0.1);
            // Click
            if (grid.clicked()) {
                chapter_name = chap.name;
                if (!chap.disabled) {
                    if (scr_chapter_new(chapter_name)) {
                        scr_load_chapter_icon(chap.icon_name, true);
                        custom = eCHAPTER_TYPE.PREMADE;
                        change_slide = 1;
                        goto_slide = 2;
                        chapter_string = chapter_name;
                        setup_chapter_trait_select();
                    } // Chapter is borked
                }
            }
        }
    }

    //* Successor Chapters *//
    grid.new_section(successor_y);

    for (var c = 0; c < array_length(successor_chapters); c++) {
        var chap = successor_chapters[c];
        i = chap.id;

        grid.new_cell();

        draw_sprite(spr_creation_icon, 0, grid.x1, grid.y1);
        draw_sprite_stretched(global.chapter_icons_map[? chap.icon_name], 0, grid.x1, grid.y1, grid.w, grid.h);

        // Hover
        if (grid.hover() && slate4 >= 30) {
            if ((old_highlight != highlight) && (highlight != i) && (goto_slide != 2)) {
                old_highlight = highlight;
                highlighting = 1;
            }
            if (goto_slide != 2) {
                highlight = i;
                tool = 1;
            }
            // Highlight on hover
            draw_rectangle_color_simple(grid.x1, grid.y1, grid.x2, grid.y2, 0, c_white, 0.1);
            //Click
            if (grid.clicked()) {
                chapter_name = chap.name;
                if (!chap.disabled) {
                    if (scr_chapter_new(chapter_name)) {
                        scr_load_chapter_icon(chap.icon_name, true);
                        custom = eCHAPTER_TYPE.PREMADE;
                        change_slide = 1;
                        goto_slide = 2;
                        chapter_string = chapter_name;
                        setup_chapter_trait_select();
                    } // borked
                }
            }
        }
    }

    //* Saved Custom Chapters *//
    grid.new_section(custom_y);
    for (var c = 0; c < array_length(custom_chapters); c++) {
        var chap = custom_chapters[c];
        i = chap.id;

        grid.new_cell();

        draw_sprite(spr_creation_icon, 0, grid.x1, grid.y1);
        if (chap.loaded == false) {
            draw_sprite_stretched(global.chapter_icons_map[? "custom_white"], 0, grid.x1, grid.y1, grid.w, grid.h);
        } else {
            var spr = scr_load_chapter_icon(chap.icon_name);
            if (is_undefined(spr)) {
                draw_sprite_stretched(global.chapter_icons_map[? "unknown"], 0, grid.x1, grid.y1, grid.w, grid.h);
            } else {
                draw_sprite_stretched(spr, 0, grid.x1, grid.y1, grid.w, grid.h);
            }
        }

        // Hover
        if (grid.hover() && slate4 >= 30) {
            if ((old_highlight != highlight) && (highlight != i) && (goto_slide != 2)) {
                old_highlight = highlight;
                highlighting = 1;
            }
            if (goto_slide != 2) {
                highlight = chap.id;
                tool = 1;
            }
            // Highlight white on hover
            draw_rectangle_color_simple(grid.x1, grid.y1, grid.x2, grid.y2, 0, c_white, 0.1);

            //Click
            if (grid.clicked()) {
                if (chap.loaded == true && chap.disabled == false) {
                    scr_load_chapter_icon(chap.icon_name, true);
                    chapter_name = chap.name;
                    global.chapter_id = chap.id;
                    change_slide = 1;
                    goto_slide = 2;
                    custom = eCHAPTER_TYPE.CUSTOM;
                    setup_chapter_trait_select();
                    scr_chapter_new(chap.id);
                } else {
                    global.chapter_id = chap.id;
                    change_slide = 1;
                    goto_slide = 2;
                    custom = eCHAPTER_TYPE.CUSTOM;
                    setup_chapter_trait_select();
                    scr_chapter_random(0);
                }
            }
        }
    }

    //* Other Chapters *//
    grid.new_section(other_y);

    for (var c = 0; c < array_length(other_chapters); c++) {
        var chap = other_chapters[c];
        i = chap.id;

        grid.new_cell();
        draw_sprite(spr_creation_icon, 0, grid.x1, grid.y1);
        draw_sprite_stretched(global.chapter_icons_map[? chap.icon_name], 0, grid.x1, grid.y1, grid.w, grid.h);

        // Hover
        if (grid.hover() && slate4 >= 30) {
            if ((old_highlight != highlight) && (highlight != i) && (goto_slide != 2)) {
                old_highlight = highlight;
                highlighting = 1;
            }
            if (goto_slide != 2) {
                highlight = i;
                tool = 1;
            }
            // Highlight white on hover
            draw_rectangle_color_simple(grid.x1, grid.y1, grid.x2, grid.y2, 0, c_white, 0.1);
            //Click
            if (grid.clicked()) {
                chapter_name = chap.name;
                if (!chap.disabled) {
                    if (scr_chapter_new(chapter_name)) {
                        scr_load_chapter_icon(chap.icon_name, true);
                        global.chapter_id = chap.id;
                        custom = eCHAPTER_TYPE.PREMADE;
                        change_slide = 1;
                        goto_slide = 2;
                        chapter_string = chapter_name;
                        setup_chapter_trait_select();
                    } // borked
                }
            }
        }
    }

    grid.new_cell();
    grid.new_cell();
    grid.new_cell(); //padding between fanmade and the custom/random buttons

    // Blank Custom Chapter
    grid.new_cell();
    draw_sprite(spr_creation_icon, 0, grid.x1, grid.y1);
    draw_sprite_stretched(global.chapter_icons_map[? "unknown"], 0, grid.x1, grid.y1, grid.w, grid.h);

    if (grid.hover() && slate4 >= 30) {
        if ((old_highlight != highlight) && (highlight != 1001) && (goto_slide != 2)) {
            old_highlight = highlight;
            highlighting = 1;
        }

        if (goto_slide != 2) {
            highlight = 1001;
            tool = 1;
        }

        draw_rectangle_color_simple(grid.x1, grid.y1, grid.x2, grid.y2, 0, c_white, 0.1);

        if (grid.clicked()) {
            scr_load_chapter_icon("unknown", true);
            change_slide = 1;
            goto_slide = 2;
            custom = eCHAPTER_TYPE.CUSTOM;
            scr_chapter_random(0);
            setup_chapter_trait_select();
        }
    }

    // Randomized Chapter
    grid.new_cell();
    draw_sprite(spr_creation_icon, 0, grid.x1, grid.y1);
    draw_sprite_stretched(global.chapter_icons_map[? "random"], 0, grid.x1, grid.y1, grid.w, grid.h);

    if (grid.hover() && slate4 >= 30) {
        if ((old_highlight != highlight) && (highlight != 1002) && (goto_slide != 2)) {
            old_highlight = highlight;
            highlighting = 1;
        }

        if (goto_slide != 2) {
            highlight = 1002;
            tool = 1;
        }

        draw_rectangle_color_simple(grid.x1, grid.y1, grid.x2, grid.y2, 0, c_white, 0.1);

        if (grid.clicked()) {
            scr_load_chapter_icon(array_random_element(global.chapter_icons_array), true);
            change_slide = 1;
            goto_slide = 2;
            custom = eCHAPTER_TYPE.RANDOM;
            scr_chapter_random(1);
            setup_chapter_trait_select();
        }
    }

    if ((tool == 1) && (highlighting < 30)) {
        highlighting += 1;
    }
    if ((tool == 0) && (highlighting > 0)) {
        highlighting -= 1;
    }

    if (((highlight > 0) && (highlighting > 0)) || ((change_slide > 0) && (goto_slide != 1))) {
        draw_set_alpha(min(slate4 / 30, highlighting / 30));
        if (change_slide > 0) {
            draw_set_alpha(1);
        }

        if (highlight == 1001) {
            scr_image("creation/chapters/splash", 97, 0, 68, 374, 713);
        }
        if (highlight == 1002) {
            scr_image("creation/chapters/splash", 98, 0, 68, 374, 713);
        }
        if (highlight <= array_length(all_chapters)) {
            var splash_chapter = all_chapters[highlight];
            scr_image("creation/chapters/splash", splash_chapter.splash, 0, 68, 374, 713);
        }

        draw_set_alpha(slate4 / 30);
        draw_set_color(CM_GREEN_COLOR);
        draw_rectangle(0, 68, 374, 781, 1);
    }
    draw_set_alpha(slate4 / 30);

    if (instance_exists(obj_cursor)) {
        obj_cursor.image_index = 0;
    }
    if ((tool == 1) && (change_slide <= 0)) {
        if (instance_exists(obj_cursor)) {
            obj_cursor.image_index = 1;
        }

        draw_set_alpha(1);
        draw_set_color(c_black);
        draw_set_halign(fa_left);

        if (highlight <= array_length(all_chapters)) {
            var chap = all_chapters[highlight];
            tooltip = localize(chap.name);
            if (chap.progenitor != 0 && chap.progenitor < 10) {
                tooltip += "  - " + localize("Progenitor: {0}", [localize(all_chapters[chap.progenitor].name)]);
            }
            tooltip2 = localize(chap.tooltip);
        }
        if (highlight == 1001) {
            tooltip = localize("Custom");
        }
        if (highlight == 1002) {
            tooltip = localize("Randomize");
        }
        if (highlight == 1001) {
            tooltip2 = localize("Create your own customized Chapter, deciding the origins, strength, and weaknesses.  Custom Chapters are weaker than Founding Chapters.");
        }
        if (highlight == 1002) {
            tooltip2 = localize("Randomly generate a Chapter to play.  The origins, strength, and weaknesses are all random.  Random Chapters are normally weaker than Founding Chapters. ");
        }
    }
}

/// @self Asset.GMObject.obj_creation
function setup_chapter_trait_select() {
    chapter_type_radio = new RadioSet([
        {
            str1: localize("Homeworld"),
            font: fnt_40k_14b,
            tooltip: localize("Homeworld\nYour Chapter has a homeworld that they base on.  Contained upon it is a massive Fortress Monastery, which provides high levels of defense and automated weapons."),
        },
        {
            str1: localize("Fleet Based"),
            font: fnt_40k_14b,
            tooltip: localize("Fleet Based\nRather than a homeworld, your Chapter begins near their recruiting world.  The fleet includes a Battle Barge, which serves as a mobile base, and powerful ship."),
        },
        {
            str1: localize("Penitent"),
            font: fnt_40k_14b,
            tooltip: localize("Penitent\nAs with Fleet Based, but you must crusade and fight until your penitence meter runs out.  Note that recruiting is disabled until then."),
        },
    ], localize("Chapter Type"), {
        x1: 445,
        y1: 211,
        max_width: 1125 - 445,
        center: true,
    });
    chapter_type_radio.current_selection = fleet_type - 1;
}

/// @self Asset.GMObject.obj_creation
function draw_chapter_trait_select() {
    draw_set_color(CM_GREEN_COLOR);
    draw_set_font(cjk_font(fnt_40k_30b));
    draw_set_halign(fa_center);

    obj_cursor.image_index = 0;

    if (name_bad == 1) {
        draw_set_color(c_red);
    }
    if ((text_selected != "chapter") || (custom != eCHAPTER_TYPE.CUSTOM)) {
        draw_text(800, 80, string_hash_to_newline(string(chapter_name)));
    }
    if (custom == eCHAPTER_TYPE.CUSTOM) {
        if ((text_selected == "chapter") && (text_bar > 30)) {
            draw_text(800, 80, string_hash_to_newline(string(chapter_name)));
        }
        if ((text_selected == "chapter") && (text_bar <= 30)) {
            draw_text(805, 80, string_hash_to_newline(string(chapter_name) + "|"));
        }
        if (scr_text_hit(800, 80, true, chapter_name)) {
            obj_cursor.image_index = 2;
            if (mouse_button_clicked()) {
                text_selected = "chapter";
                keyboard_string = chapter_name;
            }
        }
        if (text_selected == "chapter") {
            chapter_name = keyboard_string;
        }
        draw_set_alpha(0.75);
        draw_rectangle(580, 80, 1020, 118, 1);
        draw_set_alpha(1);
    }

    draw_set_color(CM_GREEN_COLOR);
    draw_text_transformed(800, 120, string_hash_to_newline(localize("Points: {0}/{1}", [points, maxpoints])), 0.6, 0.6, 0);

    obj_cursor.image_index = 0;
    if ((custom != eCHAPTER_TYPE.PREMADE) && (restarted == 0)) {
        if (scr_hit(436, 74, 564, 202) && (popup == "")) {
            obj_cursor.image_index = 1;
            if (mouse_button_clicked()) {
                popup = "icons";
            }
        }
    }

    draw_set_color(CM_GREEN_COLOR);
    draw_line(445, 200, 1125, 200);
    draw_line(445, 201, 1125, 201);
    draw_line(445, 202, 1125, 202);

    if (popup == "") {
        if (custom != eCHAPTER_TYPE.CUSTOM) {
            draw_set_alpha(0.5);
        }
        chapter_type_radio.allow_changes = custom == eCHAPTER_TYPE.CUSTOM;
        chapter_type_radio.draw();
        fleet_type = chapter_type_radio.current_selection + 1;

        draw_line(445, 289, 1125, 289);
        draw_line(445, 290, 1125, 290);
        draw_line(445, 291, 1125, 291);

        draw_set_halign(fa_center);
        draw_text_transformed(800, 301, localize("Chapter Stats"), 0.6, 0.6, 0);
        draw_set_halign(fa_left);

        draw_text_transformed(505, 332, localize("Strength: {0} ({1})", [global.chapter_strength_ratings[strength], string(strength)]), 0.5, 0.5, 0);
        draw_text_transformed(505, 387, localize("Cooperation: {0}  ({1})", [global.chapter_cooperation_ratings[cooperation], string(cooperation)]), 0.5, 0.5, 0);
        draw_text_transformed(505, 442, localize("Gene-Seed Purity: {0} ({1})", [global.chapter_geneseed_ratings[purity], string(purity)]), 0.5, 0.5, 0);
        draw_text_transformed(505, 497, localize("Gene-Seed Stability: ({0}%)", [string(stability)]), 0.5, 0.5, 0);

        var arrow_buttons_controls = [
            strength,
            cooperation,
            purity,
            stability,
        ];
        var score_costs = [
            10,
            10,
            10,
            1,
        ];
        var scores_max = [
            10,
            10,
            10,
            99,
        ];
        var scores_min = [
            1,
            1,
            1,
            1,
        ];
        var click_change = keyboard_check(vk_control) ? 10 : 1;
        if (custom == eCHAPTER_TYPE.CUSTOM) {
            for (var i = 0; i < 4; i++) {
                draw_sprite_stretched(spr_arrow, 0, 436, 325 + (i * 55), 32, 32);
                if (scr_hit(436, 325 + (i * 55), 436 + sprite_get_width(spr_arrow), 357 + (i * 55))) {
                    obj_cursor.image_index = 1;
                    tooltip = localize("Decrease");
                    tooltip2 = localize("(Hold Ctrl to decrease by 10)");
                    if (mouse_button_clicked() && (arrow_buttons_controls[i] - click_change) >= scores_min[i]) {
                        arrow_buttons_controls[i] -= click_change;
                        points -= score_costs[i] * click_change;
                    }
                }
                draw_sprite_stretched(spr_arrow, 1, 470, 325 + (i * 55), 32, 32);
                if (scr_hit(470, 325 + (i * 55), 470 + sprite_get_width(spr_arrow), 357 + (i * 55))) {
                    obj_cursor.image_index = 1;
                    tooltip = localize("Increase");
                    tooltip2 = localize("(Hold Ctrl to increase by 10)");
                    if (mouse_button_clicked() && (arrow_buttons_controls[i] + click_change) <= scores_max[i] && (points + (score_costs[i] * click_change) <= maxpoints)) {
                        arrow_buttons_controls[i] += click_change;
                        points += score_costs[i] * click_change;
                    }
                }
            }
        }

        strength = arrow_buttons_controls[0];
        cooperation = arrow_buttons_controls[1];
        purity = arrow_buttons_controls[2];
        stability = arrow_buttons_controls[3];

        if (scr_hit(505, 325, 800, 357)) {
            tooltip = localize("Strength");
            tooltip2 = localize("How many Space Marines your Chapter has. \nFor every score below five a company will be removed; conversely, each score higher grants 50 additional Astartes.");
        }
        if (scr_hit(505, 380, 800, 412)) {
            tooltip = localize("Cooperation");
            tooltip2 = localize("How diplomatic your Chapter is. \nA low score will lower starting dispositions of Imperial factions and make disposition increases less likely to occur.");
        }
        if (scr_hit(505, 435, 800, 467)) {
            tooltip = localize("Gene-Seed Purity");
            tooltip2 = localize("How many inherent mutations your gene-seed has. \nEach score below ten requires one mutation to be chosen.");
        }
        if (scr_hit(505, 490, 800, 522)) {
            tooltip = localize("Gene-Seed Stability");
            tooltip2 = localize("How easily new mutations and corruption can occur with your Chapter's gene-seed. \nAffects the amount of random mutations your existing Space Marines have, and the amount new Aspirants get after the implantation is finished.");
        }
    }

    if (popup != "icons") {
        draw_rectangle(445, 551, 1125, 553, 0);
    }

    if ((popup != "") || (custom != eCHAPTER_TYPE.CUSTOM)) {
        draw_set_alpha(0.5);
    }

    if (popup != "icons") {
        draw_selected_chapter_traits(eCHAPTER_TRAIT_TYPE.ADV);
        draw_selected_chapter_traits(eCHAPTER_TRAIT_TYPE.DISADV);

        draw_set_alpha(1);
        if (scr_hit(436, 564, 631, 583)) {
            tooltip = localize("Chapter Advantages");
            tooltip2 = localize("Advantages cost points, and improve the performance of your Chapter in a specific domain. You can only have one trait of the same category, shown in brackets.");
        }
        if (scr_hit(810, 564, 1030, 583)) {
            tooltip = localize("Chapter Disadvantages");
            tooltip2 = localize("Disadvantages grant additional points, and penalize the performance of your Chapter. You can only have one trait of the same category, shown in brackets.");
        }
    } else if (popup == "icons") {
        draw_set_alpha(1);
        draw_set_color(c_black);
        draw_rectangle(450, 206, 1144, 711, 0);

        draw_set_color(CM_GREEN_COLOR);
        draw_line(445, 727, 1125, 727);
        draw_line(445, 728, 1125, 728);
        draw_line(445, 729, 1125, 729);

        draw_set_font(cjk_font(fnt_40k_30b));
        draw_set_halign(fa_center);
        draw_text_transformed(800, 211, localize("Select an Icon"), 0.6, 0.6, 0);
        draw_text_transformed(800, 687, localize("Cancel"), 0.6, 0.6, 0);

        var cw = string_width(localize("Cancel")) * 0.6;
        var ch = string_height(localize("Cancel")) * 0.6;

        if (scr_hit(800, 687, 800 + cw, 687 + ch)) {
            draw_set_color(c_white);
            draw_set_alpha(0.25);
            draw_text_transformed(800, 687, localize("Cancel"), 0.6, 0.6, 0);
            draw_set_color(CM_GREEN_COLOR);
            draw_set_alpha(1);

            if (mouse_button_clicked()) {
                popup = "";
            }
        }

        draw_set_font(cjk_font(fnt_40k_14b));
        draw_set_halign(fa_left);

        var icons_per_row = 6; // how many icons per row

        var _surface_height = ceil(array_length(global.chapter_icons_array) / icons_per_row) * 110;
        chapter_icons_container.start_draw_to_surface(_surface_height);

        // repeat here

        var x3_start = 0;
        var y3_start = 0;

        for (var i = 0, l = array_length(global.chapter_icons_array); i < l; i++) {
            var _icon_name = global.chapter_icons_array[i];
            var _icon_sprite = global.chapter_icons_map[? _icon_name];

            var col = i % icons_per_row;
            var row = i div icons_per_row;

            var x3 = x3_start + col * 110;
            var y3 = y3_start + row * 110;

            var real_x3 = 445 + x3;
            var real_y3 = 245 + y3 - chapter_icons_container.get_scroll_offset();

            draw_sprite_stretched(_icon_sprite, 0, x3, y3, 96, 96);

            if (scr_hit(real_x3, real_y3, real_x3 + 96, real_y3 + 96)) {
                gpu_set_blendmode(bm_add);
                draw_set_alpha(0.25);
                draw_set_color(#F5F5F5);
                draw_rectangle(x3, y3, x3 + 96, y3 + 96, false);
                gpu_set_blendmode(bm_normal);
                draw_set_alpha(1);
                draw_set_color(CM_GREEN_COLOR);

                if (mouse_button_clicked()) {
                    popup = "";
                    scr_load_chapter_icon(_icon_name, true);
                    chapter_icons_container.reset_scroll_offset();
                }
            }
        }
        chapter_icons_container.stop_draw_to_surface();
        chapter_icons_container.draw(445, 245);
    }

    if (popup == "advantages") {
        draw_chapter_trait_list(eCHAPTER_TRAIT_TYPE.ADV);
    } else if (popup == "disadvantages") {
        draw_chapter_trait_list(eCHAPTER_TRAIT_TYPE.DISADV);
    }

    if (popup != "") {
        if (popup != "icons" && point_outside_and_click([445, 200, 1125, 550])) {
            popup = "";
        } else if (popup == "icons" && point_outside_and_click([445, 200, 1125, 550])) {
            popup = "";
        }
    }
}

/// @self Asset.GMObject.obj_creation
function draw_chapter_homeworld_select() {
    draw_set_color(CM_GREEN_COLOR);
    draw_set_font(cjk_font(fnt_40k_30b));
    draw_set_halign(fa_center);

    tooltip = "";
    tooltip2 = "";
    obj_cursor.image_index = 0;

    draw_text(800, 80, chapter_name);

    draw_rectangle(445, 200, 1125, 202, 0);

    scr_creation_home_planet_create();
    /// @self Asset.GMObject.obj_creation
    left_data_slate.inside_method = function() {
        if (!buttons.complex_homeworld.active) {
            var trial_data = scr_trial_data();
            var _trial_label = localize("Aspirant Trial");
            draw_text_transformed(160, 90, _trial_label, 0.6, 0.6, 0);

            if (custom == eCHAPTER_TYPE.CUSTOM) {
                var _trial_label_w = string_width(_trial_label) * 0.6;
                var _arrow_size = 32;
                var _side_gap = 10;
                var _left_x = 160 - (_trial_label_w / 2) - _side_gap - _arrow_size;
                var _right_x = 160 + (_trial_label_w / 2) + _side_gap;
                draw_sprite_stretched(spr_creation_arrow, 0, _left_x, 90, _arrow_size, _arrow_size);
                if (point_and_click([_left_x, 90, _left_x + _arrow_size, 90 + _arrow_size])) {
                    aspirant_trial++;
                    if (aspirant_trial >= array_length(trial_data)) {
                        aspirant_trial = 0;
                    }
                }
                draw_sprite_stretched(spr_creation_arrow, 1, _right_x, 90, _arrow_size, _arrow_size);
                if (point_and_click([_right_x, 90, _right_x + _arrow_size, 90 + _arrow_size])) {
                    aspirant_trial--;
                    if (aspirant_trial < 0) {
                        aspirant_trial = array_length(trial_data) - 1;
                    }
                }
            }

            var current_trial = trial_data[aspirant_trial];

            draw_text_transformed(160, 110, current_trial.name, 0.5, 0.5, 0);

            var asp_info = scr_compile_trial_bonus_string(current_trial);

            draw_set_halign(fa_center);

            draw_text_ext_transformed(160, 150, asp_info, -1, left_data_slate.width - 20, 0.4, 0.4, 0);

            if (scr_hit(50, 480, 950, 510)) {
                tooltip = localize("Aspirant Trial");
                tooltip2 = localize("A special challenge is needed for Aspirants to be judged worthy of becoming Astartes.  After completing the Trial they then become a Neophyte, beginning implantation and training.  (This can be changed once in game, but the chosen trial here will affect the spawn characteristics of your starting Space Marines).");
            }
        } else {
            draw_set_font(cjk_font(fnt_40k_30b));
            var _spawn_radio = buttons.home_spawn_loc_options;
            var _max_width = left_data_slate.width - 100;
            _spawn_radio.update({x1: 70, y1: 60, max_width: _max_width, allow_changes: custom});
            _spawn_radio.draw();

            var _warp_lanes_radio = buttons.home_warp;
            _warp_lanes_radio.update({x1: 70, y1: _spawn_radio.y2, max_width: _max_width, allow_changes: custom});
            _warp_lanes_radio.draw();

            var _home_planets = buttons.home_planets;
            _home_planets.update({x1: 70, y1: _warp_lanes_radio.y2, max_width: _max_width, allow_changes: custom});
            _home_planets.draw();
        }
    };
    left_data_slate.draw(0, 5, 0.45, 1);

    draw_rectangle(445, 640, 1125, 642, 0);

    player_select_powers();
}
