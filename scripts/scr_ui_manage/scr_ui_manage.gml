/// @self Asset.GMObject.obj_controller
function load_marines_into_ship(system, ship, units, reload = false) {
    /// @self Asset.GMObject.obj_controller
    static _load_into_ship = function(system, ship, units, size, loop, reload) {
        var load_from_star = find_star_by_name(system);
        if (is_struct(units[loop])) {
            units[loop].load_marine(sh_ide[ship], load_from_star);
            ma_loc[loop] = sh_loc[ship];
            ma_lid[loop] = sh_ide[ship];
            ma_wid[loop] = 0;
        } else if (is_array(units[loop]) && ma_loc[loop] == system && sh_loc[ship] == system) {
            var vehicle = units[loop];
            var _get = fetch_deep_array;
            var _set = alter_deep_array;
            var start_ship = _get(obj_ini.veh_lid, vehicle);
            var start_planet = _get(obj_ini.veh_wid, vehicle);
            ma_loc[loop] = sh_loc[ship];
            ma_lid[loop] = sh_ide[ship];
            ma_wid[loop] = 0;
            _set(obj_ini.veh_loc, vehicle, sh_name[ship]);
            _set(obj_ini.veh_lid, vehicle, sh_ide[ship]);
            _set(obj_ini.veh_wid, vehicle, 0);
            _set(obj_ini.veh_uid, vehicle, sh_uid[ship]);
            obj_ini.ship_carrying[sh_ide[ship]] += size;

            if (start_planet) {
                load_from_star.p_player[start_planet] -= size;
            } else if (start_ship) {
                obj_ini.ship_carrying[start_ship] -= size;
            }

            set_vehicle_last_ship(vehicle, true);
        }
    };

    for (var q = 0; q < array_length(units); q++) {
        if (man_sel[q] == 1) {
            var _unit_ship_id = -1;
            var _unit = units[q];
            var _is_marine = !is_array(_unit);
            if (!reload) {
                _unit_ship_id = ship;
            } else {
                if (_is_marine) {
                    _unit_ship_id = array_get_index(sh_uid, _unit.last_ship.uid);
                } else {
                    var last_ship_data = fetch_deep_array(obj_ini.last_ship, _unit);
                    _unit_ship_id = array_get_index(sh_uid, last_ship_data.uid);
                }
            }

            var _unit_size = 0;
            if (_is_marine) {
                _unit_size = man_size;
            } else {
                var _vehic_size = scr_unit_size("", ma_role[q], true);
                _unit_size = _vehic_size;
            }

            if (_unit_ship_id == -1) {
                if (reload) {
                    if (_is_marine) {
                        _unit.last_ship = {
                            uid: "",
                            name: "",
                        };
                    } else {
                        set_vehicle_last_ship(_unit, true);
                    }
                }
                continue;
            }
            if (_unit_ship_id < array_length(sh_cargo_max)) {
                if (sh_cargo[_unit_ship_id] + _unit_size <= sh_cargo_max[_unit_ship_id]) {
                    _load_into_ship(system, _unit_ship_id, units, _unit_size, q, reload);
                    man_sel[q] = 0;
                }
            }
        }
    }
    system = "";
    man_size = 0;
    man_current = 0;
    if (reload == false) {
        menu = eMENU.MANAGE;
    }
    selecting_ship = -1;
    if (managing == -1 && obj_controller.selection_data.purpose != "Ship Management") {
        update_adhoc_manage();
    }
}

/// @desc Displays a selectable prompt for special roles to be assigned.
/// @param {struct} search_params - Criteria for the role search
/// @param {struct} role_group_params - Parameters defining the role group
/// @param {string} purpose - Display purpose for the selection
/// @param {string} purpose_code - Code that identifies the selection’s purpose
function command_slot_prompt(search_params, role_group_params, purpose, purpose_code) {
    var candidates = collect_role_group(role_group_params.group, role_group_params.location, role_group_params.opposite, search_params);
    group_selection(candidates, {purpose: purpose, purpose_code: purpose_code, number: 1, target_company: managing, feature: "none", planet: 0, selections: []});
}

/// @desc Displays a selectable prompt for special roles to be assigned.
/// @param {number} xx - X coordinate for the UI element
/// @param {number} yy - Y coordinate for the UI element
/// @param {string} slot_text - The prompt text displayed in the UI
function command_slot_draw(xx, yy, slot_text) {
    draw_set_color(c_black);
    draw_rectangle(xx + 25, yy + 64, xx + 974, yy + 85, 0);
    draw_set_color(c_gray);
    draw_rectangle(xx + 25, yy + 64, xx + 974, yy + 85, 1);
    draw_set_halign(fa_center);
    draw_set_color(c_yellow);
    draw_text(xx + 500, yy + 66, $"++{slot_text}++");
    draw_set_halign(fa_left);
    draw_set_color(c_gray);
    if (point_and_click([xx + 25, yy + 64, xx + 974, yy + 85])) {
        return true;
    } else {
        return false;
    }
}

/// @self Asset.GMObject.obj_controller
/// @param {Struct.TTRPG_stats} unit
function reset_manage_unit_constants(unit) {
    try {
        if (is_struct(unit_manage_constants)) {
            gc_struct(unit_manage_constants);
            delete unit_manage_constants;
        }

        unit_manage_constants = {};

        fix_right = 0;

        var _equip_data = unit.unit_equipment_data();
        unit_manage_constants.faction_owner = "1";
        if (unit.race() != 1) {
            unit_manage_constants.owner = unit.race();
        }
        unit_manage_constants.current_data = unit.uid;
        var _damage_res = unit.damage_resistance();

        var _slot_defs = global.unit_equip_slots;

        for (var i = 0; i < 5; i++) {
            var _slot = _slot_defs[i];
            var _const_key = $"{_slot}_string";
            unit_manage_constants[$ _const_key] = _equip_data.equipment_ReactiveString(_slot);
        }

        // Psyker things
        var _psionic = "";
        var _psy_powers_known = unit.powers_known;
        var _psy_powers_count = array_length(_psy_powers_known);
        var _tooltip = "";
        if (_psy_powers_count > 0) {
            _psionic = $"{unit.psionic}/{_psy_powers_count}";
            _tooltip = generate_marine_powers_description_string(unit);
        }

        // Corruption
        if ((obj_controller.chaos_rating > 0) && (_psionic != "")) {
            _psionic = localize("{0}\n{1}% Corruption.", [_psionic, max(0, unit.corruption())]);
        }

        unit_manage_constants.psy = new LabeledIcon(spr_icon_psyker, _psionic, 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: localize("==Psychic Stats==\n{0}", [_tooltip]),
        });
        // Damage Resistance

        var _res_tool = "Health damage taken by the marine is reduced by this percentage. This happens after the flat reduction from armor.\n\nContributing factors:\n";
        _res_tool += _equip_data.set_attribute_string("damage_resistance_mod");
        _res_tool += $"CON: {round(unit.constitution / 2)}%";

        unit_manage_constants.damage_res = new LabeledIcon(spr_icon_iron_halo, $"{_damage_res}%", 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: _res_tool,
        });
        var _hp_val = $"{round(unit.hp())}/{unit.max_health()}";
        var _hp_tool = localize("A measure of how much punishment the creature can take. Marines can go into the negatives and still survive, but they'll require a bionic to become fighting fit once more.\n\nContributing factors:\n");
        _hp_tool += localize("CON: {0}\n", [unit.constitution * 3]);

        _hp_tool += _equip_data.set_attribute_string("hp_mod");

        unit_manage_constants.hp = new LabeledIcon(spr_icon_health, _hp_val, 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: _hp_tool,
        });

        // -------------------------
        // Armour Rating
        // -------------------------
        var _armour_val = $"{unit.armour_calc()}";
        var _armour_tool = localize("Reduces incoming damage at a flat rate. Certain enemies may attack in ways that may bypass your armor entirely, for example power weapons and some warp sorceries.\n\nContributing factors:\n");

        _armour_tool += _equip_data.set_attribute_string("armour_value");

        if (obj_controller.stc_bonus[1] == 5 || obj_controller.stc_bonus[2] == 3) {
            _armour_tool += localize("STC Bonus: x1.05\n");
        }

        unit_manage_constants.armour = new LabeledIcon(spr_icon_shield2, _armour_val, 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: _armour_tool,
        });

        unit_manage_constants.exp = new LabeledIcon(spr_icon_veteran, string(floor(unit.experience)), 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: localize("==Experience==\nA measurement of how battle-hardened the unit is. Provides various bonuses across the board. Every 15 EXP, a new stat is assigned. Hover over the unit's stats in the marine profile to see projected growth over time."),
        });

        // Melee Attack
        var _melee = unit.melee_attack();
        unit_manage_constants.melee_attack = new LabeledIcon(spr_icon_weapon_skill, $"{round(_melee[0])}", 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: localize("==Melee Attack==\n{0}", [_melee[1]]),
            colour: unit.encumbered_melee ? #bf4040 : CM_GREEN_COLOR,
        });

        var _carry = _melee[2];
        unit_manage_constants.melee_burden = new LabeledIcon(spr_icon_weight, $"{_carry[0]}/{_carry[1]}", 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: localize("==Melee Burden==\n{0}", [_carry[2]]),
            colour: unit.encumbered_melee ? #bf4040 : CM_GREEN_COLOR,
        });

        // Ranged Attack
        var _range = unit.ranged_attack();
        unit_manage_constants.ranged_attack = new LabeledIcon(spr_icon_ballistic_skill, $"{round(_range[0])}", 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: localize("==Ranged Attack==\n{0}", [_range[1]]),
            colour: unit.encumbered_ranged ? #bf4040 : CM_GREEN_COLOR,
        });

        _carry = _range[2];
        unit_manage_constants.ranged_burden = new LabeledIcon(spr_icon_weight, $"{_carry[0]}/{_carry[1]}", 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: localize("==Ranged Burden==\n{0}", [_carry[2]]),
            colour: unit.encumbered_ranged ? #bf4040 : CM_GREEN_COLOR,
        });

        // -------------------------
        // Bionics
        // -------------------------
        var _bionic_val = $"{unit.bionics}";
        var _bionic_tool = localize("Bionic Augmentation is something a unit can do to both enhance their capabilities, but also replace a missing limb to get back into the fight.");
        _bionic_tool += localize("\nThere is a limit of 10 Bionic augmentations. After that the damage is so extensive that a marine requires a dreadnought to keep going.");
        _bionic_tool += localize("\nFor everyone else? It's time for the emperor's mercy.");
        _bionic_tool += localize("\n\nCurrent Bionic Augmentations:\n");

        var _body_parts = global.unit_body_parts;
        var _body_parts_display = global.unit_body_parts_display;

        for (var part = 0; part < array_length(_body_parts); part++) {
            if (struct_exists(unit.body[$ _body_parts[part]], "bionic")) {
                var part_display = _body_parts_display[part];
                _bionic_tool += localize("Bionic {0}", [localize(part_display)]);
                switch (part_display) {
                    case "Left Leg":
                    case "Right Leg":
                        _bionic_tool += localize(" (CON: +2 STR: +1 DEX: -2)\n");
                        break;
                    case "Left Eye":
                    case "Right Eye":
                        _bionic_tool += localize(" (CON: +1 WIS: +1 DEX: +1)\n");
                        break;
                    case "Left Arm":
                    case "Right Arm":
                        _bionic_tool += localize(" (CON: +2 STR: +2 WS: -1)\n");
                        break;
                    case "Torso":
                        _bionic_tool += localize(" (CON: +4 STR: +1 DEX: -1)\n");
                        break;
                    case "Throat":
                        _bionic_tool += localize(" (CHA: -1)\n");
                        break;
                    case "Jaw":
                    case "Head":
                        _bionic_tool += localize(" (CON: +1)\n");
                        break;
                }
            }
        }

        unit_manage_constants.bionics = new LabeledIcon(spr_icon_bionics, _bionic_val, 0, 0, {
            icon_width: 24,
            icon_height: 24,
            tooltip: _bionic_tool,
        });

        if (is_struct(unit_manage_image)) {
            try {
                unit_manage_image.destroy_image();
            }
            delete unit_manage_image;
        }

        unit_manage_image = unit.draw_unit_image();

        temp[122] = unit.handle_stat_growth();

        var _string_data = {
            colour: #50a076,
            scale: 0.7,
            halign: fa_center,
            font: fnt_40k_30b,
            scale_text: true,
            max_width: 250,
            min_scale: 0.7,
        };

        var _name = unit.name_role(true, false);

        unit_manage_constants.name = new ReactiveString(_name, 0, 0, _string_data);

        var _role_name = "";

        var _comp_string = "";

        if (unit.company <= 0) {
            _role_name = localize(unit.squad_role());
        } else if (unit.IsSpecialist()) {
            _comp_string = localize("{0} Company", [unit.company_roman()]);
            _role_name = localize(unit.role());
        } else {
            _comp_string = localize("{0} Company", [unit.company_roman()]);
            _role_name = localize(unit.squad_role());
        }

        _string_data = {
            colour: #50a076,
            scale: 1,
            halign: fa_center,
            font: fnt_40k_14b,
            scale_text: true,
            max_width: 250,
        };

        unit_manage_constants.role_name = new ReactiveString(_role_name, 0, 0, _string_data);

        _string_data = {
            colour: #50a076,
            scale: 1,
            halign: fa_center,
            font: fnt_40k_14b,
            scale_text: true,
            max_width: 250,
        };

        unit_manage_constants.company_string = new ReactiveString(_comp_string, 0, 0, _string_data);

        // TODO
    } catch (_exception) {
        ERROR_HANDLER.handle_exception(_exception);
        obj_controller.unit_focus = undefined;
    } //not sure handling with normal method exception could just be a pain here
}

/// @self Asset.GMObject.obj_controller
function company_specific_management() {
    add_draw_return_values();
    draw_set_font(cjk_font(fnt_40k_30b));
    draw_set_halign(fa_center);
    draw_set_color(c_gray); // CM_GREEN_COLOR
    var _allow_shorts = false;
    var _comp = "";
    if (managing > 20) {
        _comp = managing - 10;
    } else if ((managing >= 1) && (managing <= 10)) {
        _company_name = localize("{0} Company", [int_to_roman(managing)]);
        _comp = managing;
    } else if (managing > 10) {
        switch (managing) {
            case 11:
                _company_name = localize("Headquarters");
                break;
            case 12:
                _company_name = localize("Apothecarion");
                break;
            case 13:
                _company_name = localize("Librarium");
                break;
            case 14:
                _company_name = localize("Reclusium");
                break;
            case 15:
                _company_name = localize("Armamentarium");
                break;
        }
    }
    // Draw the company followed by chapters name
    draw_text(800, 64, localize("{0}, {1}", [_company_name, global.chapter_name]));
    if (managing <= 10) {
        var _text_input = management_buttons.company_namer;

        obj_ini.company_title[managing] = _text_input.draw(obj_ini.company_title[managing]);
        _allow_shorts = !_text_input.allow_input;
    } else {
        _allow_shorts = true;
    }
    if (allow_shortcuts) {
        allow_shortcuts = _allow_shorts;
    }
    pop_draw_return_values();
}

/// @self Asset.GMObject.obj_controller
function alternative_manage_views(x1, y1) {
    //for some reason management_buttons keeps dying so hopefully this will solve the issue until something better can be found
    if (!is_struct(management_buttons)) {
        init_manage_buttons();
    }
    var _squad_button = management_buttons.squad_toggle;
    _squad_button.update({x1: x1 + 5, y1: y1 + 6, label: !obj_controller.view_squad && !obj_controller.company_report ? "Squad View" : "Company View", keystroke: keyboard_check_pressed(ord("S")) && allow_shortcuts});

    if (company_data.has_squads) {
        if (_squad_button.draw(!text_bar)) {
            view_squad = !view_squad;
            if (view_squad) {
                new_company_struct();
            }
        }
    }

    if (!view_squad) {
        var _profile_toggle = management_buttons.profile_toggle;
        _profile_toggle.update({label: !unit_profile ? "Show Profile" : "Hide Profile", x1: _squad_button.x2, y1: _squad_button.y1, keystroke: keyboard_check_pressed(ord("P")) && allow_shortcuts});
        if (_profile_toggle.draw(!text_bar)) {
            unit_profile = !unit_profile;
        }

        if (unit_profile) {
            var bio_toggle = management_buttons.bio_toggle;
            bio_toggle.update({label: !unit_bio ? "Show Bio" : "Hide Bio", x1: _profile_toggle.x2, y1: _profile_toggle.y1, keystroke: keyboard_check_pressed(ord("B")) && allow_shortcuts});
            if (bio_toggle.draw(!text_bar)) {
                unit_bio = !unit_bio;
            }
        }
    }
    // === Capture Image Button ===
    var _last_button_x = _squad_button.x2; // default if no profile/bio drawn
    var _last_button_y = _squad_button.y1;

    // if profile is visible, place after profile toggle
    if (!view_squad) {
        _last_button_x = management_buttons.profile_toggle.x2;
        _last_button_y = management_buttons.profile_toggle.y1;

        // if bio visible, place after bio toggle
        if (unit_profile) {
            _last_button_x = management_buttons.bio_toggle.x2;
            _last_button_y = management_buttons.bio_toggle.y1;
        }
    }

    var _capture_button = management_buttons.capture_image;
    _capture_button.update({x1: _last_button_x, y1: _last_button_y});

    if (is_struct(obj_controller.unit_focus) && _capture_button.draw(!text_bar)) {
        // Capture the sprite frame as PNG
        var spr = obj_controller.unit_manage_image.unit_sprite;
        var _unit = obj_controller.unit_focus; //unit struct
        if (sprite_exists(spr)) {
            var w = sprite_get_width(spr);
            var h = sprite_get_height(spr);

            // create surface and draw sprite frame 0
            var surf = surface_create(w, h);
            surface_set_target(surf);
            draw_clear_alpha(c_black, 0);
            draw_sprite(spr, 0, 0, 0);
            surface_reset_target();

            // save to local game folder
            var main_dir = working_directory + "/Screenshots";
            file_ensure_directory(main_dir);
            var base_name = main_dir + $"/marine_capture_{_unit.name()}_id{_unit.marine_number}_co{_unit.company}";
            var extension = ".png";
            var index = 0;
            var path;

            for (var i = 0; i <= 1000; i++) {
                // safety limit
                path = base_name + string(index) + extension;
                if (!file_exists(path)) {
                    break;
                }
                index++;
            }
            surface_save(surf, path);

            // cleanup
            surface_free(surf);

            if (file_exists(path)) {
                LOGGER.debug("Marine image saved to: " + path);
            } else {
                LOGGER.error("Failed to save marine image to: " + path);
            }
        }
    }
}

/// @self Asset.GMObject.obj_controller
function draw_sprite_and_unit_equip_data() {
    draw_set_font(cjk_font(fnt_40k_14));
    draw_set_halign(fa_left);
    // Swap between squad view and normal view
    company_data.unit_ui_panel.inside_method = function() {
        var _unit_tooltips = [];
        draw_set_color(c_gray);
        var xx = company_data.unit_ui_panel.XX;
        var yy = company_data.unit_ui_panel.YY;
        // draw_line(xx+1005,yy+519,xx+1576,yy+519);
        draw_set_font(cjk_font(fnt_40k_14b));
        if (is_struct(obj_controller.unit_focus)) {
            var selected_unit = obj_controller.unit_focus; //unit struct
            if (selected_unit.uid != unit_manage_constants.current_data) {
                reset_manage_unit_constants(selected_unit);
            }
            ///tooltip_text stacks hover over type tooltips into an array and draws them last so as not to create drawing order issues
            draw_set_color(c_red);
            var no_other_instances = !instance_exists(obj_temp3) && !instance_exists(obj_popup);
            var stat_tool_tip_text;
            var button_coords;
            var _allow_alternative_views = managing >= 0;
            if (!_allow_alternative_views) {
                _allow_alternative_views = is_struct(selection_data) && selection_data.purpose_code == "manage";
            }
            if (_allow_alternative_views) {
                alternative_manage_views(xx + 5, yy + 6);
            }

            if (!_allow_alternative_views) {
                unit_profile = true;
            }
            //TODO Implement company report
            /*var x6=x5+string_width(stat_tool_tip_text)+4;
            var y6=y5+string_height(stat_tool_tip_text)+2;        
            draw_unit_buttons([x5,y5,x6,y6], stat_tool_tip_text,[1,1],c_red);
            if (company_data!={}){
                array_push(company_data.tooltip_drawing, ["click or press R to show Company Report", [x5,y5,x6,y6]]);
                if ((keyboard_check_pressed(ord("R"))|| (point_in_rectangle(mouse_x, mouse_y,x5,y5,x6,y6) && mouse_check_button_pressed(mb_left))) && !instance_exists(obj_temp3) && !instance_exists(obj_popup)){
                    view_squad =false;
                    unit_profile=false;
                    company_report = !company_report;
                }
            }else{
                draw_set_alpha(0.5);
                draw_set_color(c_black);
                draw_rectangle(x5,y5,x6,y6,0);
                draw_set_alpha(1);
            }
            */

            // Draw unit image
            draw_set_color(c_white);
            if (is_struct(obj_controller.unit_manage_image)) {
                obj_controller.unit_manage_image.draw(xx + 320, yy + 109);
            }

            //TODO implement tooltip explaining potential loyalty hit of demoting a sgt
            // Sergeant promotion button
            if (view_squad && company_data.has_squads) {
                if (company_data.current_squad != -1) {
                    var cur_squad = company_data.grab_current_squad();
                    var sgt_possible = cur_squad.type != "command_squad" && !selected_unit.IsSpecialist(SPECIALISTS_SQUAD_LEADERS);
                    if (!is_struct(cur_squad.squad_leader) || selected_unit.uid != cur_squad.squad_leader.uid) {
                        if (point_and_click(draw_unit_buttons([xx + 200 + 50, yy + 329], localize("Make Sgt"), [1, 1], #50a076,,, sgt_possible ? 1 : 0.5)) && sgt_possible) {
                            cur_squad.change_sgt(selected_unit);
                        }
                    }
                }
            }

            // Unit window entries start
            var line_color = #50a076;
            draw_set_color(line_color);

            // Draw unit name and role

            unit_manage_constants.name.update({x1: xx + 402, y1: yy + 76});

            unit_manage_constants.role_name.update({x1: xx + 402, y1: yy + 56});

            unit_manage_constants.company_string.update({x1: xx + 402, y1: yy + 36});

            unit_manage_constants.name.draw();
            unit_manage_constants.role_name.draw();
            unit_manage_constants.company_string.draw();

            // Draw unit info
            draw_set_font(cjk_font(fnt_40k_14));
            // Left side of the screen
            draw_set_halign(fa_left);
            var x_left = xx + 22;

            // Equipment
            var _armour = unit_manage_constants.armour_string;

            _armour.update({x1: x_left, y1: yy + 179});

            _armour.draw();

            var _gear = unit_manage_constants.gear_string;

            _gear.update({x1: x_left, y1: yy + 305});

            _gear.draw();

            var _mobi = unit_manage_constants.mobi_string;

            _mobi.update({x1: x_left, y1: yy + 326});

            _mobi.draw();

            var _wep1 = unit_manage_constants.wep1_string;

            _wep1.update({x1: x_left, y1: yy + 204});

            _wep1.draw();

            var _wep2 = unit_manage_constants.wep2_string;

            _wep2.update({x1: x_left, y1: yy + 254});

            _wep2.draw();

            // Stats
            // Bionics trackers
            unit_manage_constants.bionics.update({x1: x_left + 84, y1: yy + 63});
            unit_manage_constants.bionics.draw();

            unit_manage_constants.armour.update({x1: x_left - 6, y1: yy + 87});
            unit_manage_constants.armour.draw();

            unit_manage_constants.hp.update({x1: x_left - 6, y1: yy + 63});
            unit_manage_constants.hp.draw();

            // Experience
            unit_manage_constants.exp.update({x1: x_left - 6, y1: yy + 39});
            unit_manage_constants.exp.draw();

            unit_manage_constants.damage_res.update({x1: x_left + 84, y1: yy + 87});

            unit_manage_constants.damage_res.draw();

            // Psyker things

            if (array_length(selected_unit.powers_known)) {
                unit_manage_constants.psy.update({x1: x_left + 84, y1: yy + 39});

                unit_manage_constants.psy.draw();
            }

            unit_manage_constants.melee_attack.update({x1: x_left - 6, y1: yy + 111});

            unit_manage_constants.melee_attack.draw();

            unit_manage_constants.ranged_attack.update({x1: x_left - 6, y1: yy + 135});

            unit_manage_constants.ranged_attack.draw();

            unit_manage_constants.melee_burden.update({x1: x_left + 84, y1: yy + 111});

            unit_manage_constants.melee_burden.draw();

            unit_manage_constants.ranged_burden.update({x1: x_left + 84, y1: yy + 135});

            unit_manage_constants.ranged_burden.draw();
        }
        setup_tooltip_list(_unit_tooltips);
    };
    if (!instance_exists(obj_popup)) {
        company_data.unit_ui_panel.draw_with_dimensions();
    }
}

/// @self Asset.GMObject.obj_controller
function scr_ui_manage() {
    if (combat != 0) {
        exit;
    }
    // This is the draw script for showing the main management screen or individual company screens

    if ((zoomed == 0) && (menu == 1) && (managing >= 0)) {
        if (managing > 0) {
            if (managing == 14){
                if (scr_has_adv("Spiritual Healers")){
                    managing = 12;
                }
            }
            company_manage_actions();
        }
        if (allow_shortcuts) {
            ui_manage_hotkeys();
        }
    }

    if ((menu == 1) && (managing > 0 || managing < 0)) {
        if (!mouse_check_button(mb_left)) {
            drag_square = [];
            rectangle_action = -1;
        }
        if (squad_sel_count > 0) {
            squad_sel_count--;
        }
        if (squad_sel_count == 0) {
            squad_sel = -1;
            squad_sel_action = -1;
        }
        if (man_size < 1) {
            reset_manage_selections();
        }
        var unit;
        var x1;
        var x2;
        var x3;
        var y1;
        var y2;
        var y3;
        var text;
        var tooltip_text = "";
        var bionic_tooltip = "";
        company_data.tooltip_drawing = [];
        var xx = camera_get_view_x(view_camera[0]);
        var yy = camera_get_view_y(view_camera[0]);
        var bb = "";
        var img = 0;

        // Draw BG
        draw_set_alpha(1);
        draw_sprite(spr_rock_bg, 0, xx, yy);
        draw_set_font(cjk_font(fnt_40k_30b));
        draw_set_halign(fa_center);
        draw_set_color(c_gray); // CM_GREEN_COLOR

        // Var declarations
        var c = 0;
        var _company_name = "";
        var skin = obj_ini.skin_color;
        static stats_displayed = false;

        if (managing < 0 && selection_data != false) {
            if (struct_exists(selection_data, "purpose")) {
                draw_text(xx + 800, yy + 74, localize(selection_data.purpose));
            }
            if (selection_data.select_type == eMISSION_SELECT_TYPE.SQUADS) {
                view_squad = true;
            }
        }

        draw_set_font(cjk_font(fnt_40k_14));

        if (managing >= 0) {
            // Draw arrows
            draw_sprite_ext(spr_arrow, 0, xx + 25, yy + 70, 2, 2, 0, c_white, 1); // Back
            draw_sprite_ext(spr_arrow, 0, xx + 429, yy + 70, 2, 2, 0, c_white, 1); // Left
            draw_sprite_ext(spr_arrow, 1, xx + 1110, yy + 70, 2, 2, 0, c_white, 1); // Right
        }
        right_ui_block = {
            x1: xx + 1008,
            y1: yy + 141,
            w: 568,
            h: 681,
        };
        right_ui_block.x2 = right_ui_block.x1 + right_ui_block.w;
        right_ui_block.y2 = right_ui_block.y1 + right_ui_block.h;

        actions_block = {
            x1: right_ui_block.x1,
            y1: yy + 520,
            w: 569,
            h: 302,
        };
        actions_block.x2 = actions_block.x1 + actions_block.w;
        actions_block.y2 = actions_block.y1 + actions_block.h;

        draw_sprite_stretched(spr_data_slate_back, 0, actions_block.x1, actions_block.y1, actions_block.w, actions_block.h);
        draw_rectangle_color_simple(actions_block.x1, actions_block.y1, actions_block.x2, actions_block.y2, 1, c_gray);
        draw_rectangle_color_simple(actions_block.x1 + 1, actions_block.y1 + 1, actions_block.x2 - 1, actions_block.y2 - 1, 1, c_black);
        draw_rectangle_color_simple(actions_block.x1 + 2, actions_block.y1 + 2, actions_block.x2 - 2, actions_block.y2 - 2, 1, c_gray);

        draw_set_color(c_white);
        // Back

        draw_set_halign(fa_left);
        var top = man_current, sel = top, temp1 = "", temp2 = "", temp3 = "", temp4 = "";

        yy += 77;

        //TODO store these in global tooltip storage
        potential_tooltip = [];
        health_tooltip = [];
        promotion_tooltip = [];

        //tooltip text to tell you if a unit is eligible for special roles

        get_command_slots_data = function() {
            var _command_slots_data = [
                {
                    search_params: {
                        companies: managing,
                    },
                    role_group_params: {
                        group: SPECIALISTS_CAPTAIN_CANDIDATES,
                        location: "",
                        opposite: false,
                    },
                    purpose: localize("{0} Company Captain Candidates", [int_to_roman(managing)]),
                    purpose_code: "captain_promote",
                    button_text: localize("New Captain Required"),
                    unit_check: "captain",
                },
                {
                    search_params: {
                        stat: [
                            [
                                "weapon_skill",
                                44,
                                "more",
                            ],
                        ],
                        companies: managing,
                    },
                    role_group_params: {
                        group: SPECIALISTS_CAPTAIN_CANDIDATES,
                        location: "",
                        opposite: false,
                    },
                    purpose: localize("{0} Company Champion Candidates", [int_to_roman(managing)]),
                    purpose_code: "champion_promote",
                    button_text: localize("Champion Required"),
                    unit_check: "champion",
                },
                {
                    search_params: {
                        companies: managing,
                    },
                    role_group_params: {
                        group: SPECIALISTS_CAPTAIN_CANDIDATES,
                        location: "",
                        opposite: false,
                    },
                    purpose: localize("{0} Company Ancient Candidates", [int_to_roman(managing)]),
                    purpose_code: "ancient_promote",
                    button_text: localize("Ancient Required"),
                    unit_check: "ancient",
                },
                {
                    search_params: {
                        companies: [
                            managing,
                            0,
                        ],
                    },
                    role_group_params: {
                        group: [
                            SPECIALISTS_CHAPLAINS,
                            false,
                            false,
                        ],
                        location: "",
                        opposite: false,
                    },
                    purpose: localize("{0} Company Chaplain Candidates", [int_to_roman(managing)]),
                    purpose_code: "chaplain_promote",
                    button_text: localize("Chaplain Required"),
                    unit_check: "chaplain",
                },
                {
                    search_params: {
                        companies: [
                            managing,
                            0,
                        ],
                    },
                    role_group_params: {
                        group: [
                            SPECIALISTS_APOTHECARIES,
                            false,
                            false,
                        ],
                        location: "",
                        opposite: false,
                    },
                    purpose: localize("{0} Company Apothecary Candidates", [int_to_roman(managing)]),
                    purpose_code: "apothecary_promote",
                    button_text: localize("Apothecary Required"),
                    unit_check: "apothecary",
                },
                {
                    search_params: {
                        companies: [
                            managing,
                            0,
                        ],
                    },
                    role_group_params: {
                        group: [
                            SPECIALISTS_TECHS,
                            false,
                            false,
                        ],
                        location: "",
                        opposite: false,
                    },
                    purpose: localize("{0} Company Tech Marine Candidates", [int_to_roman(managing)]),
                    purpose_code: "tech_marine_promote",
                    button_text: localize("Tech Marine Required"),
                    unit_check: "tech_marine",
                },
            ];

            if (!scr_has_disadv("Psyker Intolerant")) {
                array_push(_command_slots_data, {search_params: {companies: [managing, 0]}, role_group_params: {group: [SPECIALISTS_LIBRARIANS, false, false], location: "", opposite: false}, purpose: localize("{0} Company Librarian Candidates", [int_to_roman(managing)]), purpose_code: "librarian_promote", button_text: localize("Librarian Required"), unit_check: "lib"});
            }

            return _command_slots_data;
        };

        if (!obj_controller.view_squad) {
            var repetitions = min(man_max, MANAGE_MAN_SEE);
            man_count = 0;

            var _command_slots_data = get_command_slots_data();
            draw_set_font(cjk_font(fnt_40k_14));
            if (managing > 0 && managing <= 10) {
                for (var r = 0; r < array_length(_command_slots_data); r++) {
                    var role = _command_slots_data[r];
                    if (company_data[$ role.unit_check] == "none") {
                        var _clicked = command_slot_draw(xx, yy, role.button_text);
                        if (_clicked) {
                            command_slot_prompt(role.search_params, role.role_group_params, role.purpose, role.purpose_code);
                        }
                        yy += 20;
                        if (managing == -1) {
                            exit;
                        }
                        repetitions--;
                    }
                }
            }

            var _only_display_selected = instance_exists(obj_popup) && (obj_popup.type == 5 || obj_popup.type == 5.1 || obj_popup.type == 6);
            for (var i = 0; i < max(0, repetitions); i++) {
                draw_set_font(cjk_font(fnt_40k_14));
                if (sel >= array_length(display_unit)) {
                    break;
                }

                while ((sel < array_length(display_unit)) && (man[sel] == "hide" || (man_sel[sel] != 1 && _only_display_selected))) {
                    sel += 1;
                }
                if (sel >= array_length(display_unit)) {
                    break;
                }
                if (scr_draw_management_unit(sel, yy, xx, true, _only_display_selected) == "continue") {
                    sel++;
                    i--;
                    continue;
                }
                if (i == 0) {
                    if (point_and_click([xx + 25 + 8, yy + 64, xx + 974, yy + 85])) {
                        man_current = man_current > 0 ? man_current - 1 : 0;
                    }
                } else if (i == repetitions - 1) {
                    if (point_and_click([xx + 25 + 8, yy + 64, xx + 974, yy + 85])) {
                        man_current = man_current < man_max - MANAGE_MAN_SEE ? man_current + 1 : man_current == (man_max - MANAGE_MAN_SEE);
                        man_current++;
                    }
                }
                yy += 20;
                sel += 1;
            }
            if (sel_all != "" || squad_sel_count > 0) {
                for (var i = 0; i < top; i++) {
                    scr_draw_management_unit(i, yy, xx, false);
                }
                for (var i = sel; i < array_length(display_unit); i++) {
                    scr_draw_management_unit(i, yy, xx, false);
                }
            }
            sel_all = "";

            draw_set_color(c_black);
            draw_rectangle(xx + 974, yy + 165, xx + 1005, yy + 822, 0);
            draw_set_color(c_gray);
            draw_rectangle(xx + 974, yy + 165, xx + 1005, yy + 822, 1);

            // Squad outline
            draw_rectangle(xx + 25, yy + 142, xx + 14 + 8, yy + 822, 1);

            draw_set_color(0);
            draw_rectangle(xx + 974, yy + 141, xx + 1005, yy + 172, 0);
            draw_rectangle(xx + 974, yy + 790, xx + 1005, yy + 822, 0);
            draw_set_color(c_gray);
            draw_rectangle(xx + 974, yy + 141, xx + 1005, yy + 172, 1);
            draw_rectangle(xx + 974, yy + 790, xx + 1005, yy + 822, 1);

            draw_sprite_stretched(spr_arrow, 2, xx + 974, yy + 141, 31, 30);
            draw_sprite_stretched(spr_arrow, 3, xx + 974, yy + 791, 31, 30);

            var _draw_selec_buttons = !obj_controller.unit_profile && !stats_displayed;
            if (_draw_selec_buttons && instance_exists(obj_popup)) {
                _draw_selec_buttons = obj_popup.type != ePOPUP_TYPE.EQUIP;
            }
            if (_draw_selec_buttons && is_struct(obj_controller.unit_focus)) {
                draw_manage_selection_buttons();
            }

            draw_set_color(#3f7e5d);
            scr_scrollbar(974, 172, 1005, 790, 34, man_max, man_current);
        }
        if (instance_exists(obj_controller) && is_struct(obj_controller.unit_focus)) {
            var selected_unit = obj_controller.unit_focus;
            if (selected_unit.race() != 0) {
                draw_set_alpha(1);
                if (obj_controller.unit_profile && !instance_exists(obj_popup)) {
                    stats_displayed = true;
                    selected_unit.stat_display(true);
                } else {
                    stats_displayed = false;
                }

                with (obj_controller) {
                    if (view_squad && !instance_exists(obj_popup)) {
                        if (managing > 10) {
                            view_squad = false;
                            unit_profile = false;
                        } else if (company_data.has_squads) {
                            unit_profile = true;
                            try {
                                company_data.draw_squad_view();
                            } catch (_exception) {
                                ERROR_HANDLER.handle_exception(_exception);
                                obj_controller.view_squad = false;
                                obj_controller.unit_profile = false;
                            }
                        }
                    }
                }
            }
        }
        setup_tooltip_list(company_data.tooltip_drawing);
    } else if (menu == 30 && (managing > 0 || managing == -1)) {
        // Load to ships
        var bb = "";
        var img = 0;

        var xx = camera_get_view_x(view_camera[0]);
        var yy = camera_get_view_y(view_camera[0]);

        // BG
        draw_set_alpha(1);
        draw_sprite(spr_rock_bg, 0, xx, yy);
        draw_set_font(cjk_font(fnt_40k_30b));
        draw_set_halign(fa_center);
        draw_set_color(c_gray); // CM_GREEN_COLOR

        // Draw Title
        var c = 0;
        var fx = "";
        if (managing <= 10) {
            c = managing;
        }
        if (managing > 20) {
            c = managing - 10;
        }

        // Draw companies
        if (managing > 0) {
            if (managing >= 1 && managing <= 10) {
                fx = localize("{0} Company", [int_to_roman(managing)]);
            } else if (managing > 10) {
                switch (managing) {
                    case 11:
                        fx = localize("Headquarters");
                        break;
                    case 12:
                        fx = localize("Apothecarion");
                        break;
                    case 13:
                        fx = localize("Librarium");
                        break;
                    case 14:
                        fx = localize("Reclusium");
                        break;
                    case 15:
                        fx = localize("Armamentarium");
                        break;
                    default:
                        fx = localize("Unknown");
                        break;
                }
            }
        }

        draw_text(xx + 800, yy + 74, localize("{0} {1}", [global.chapter_name, fx]));

        if (managing >= 0 && managing <= 10) {
            if (obj_ini.company_title[managing] != "") {
                draw_set_font(cjk_font(fnt_fancy));
                draw_text(xx + 800, yy + 110, localize("''{0}''", [obj_ini.company_title[managing]]));
            }
        }

        // Back
        draw_sprite_ext(spr_arrow, 0, xx + 25, yy + 70, 2, 2, 0, c_white, 1);

        if (point_and_click([xx + 25, yy + 70, xx + 70, yy + 140])) {
            man_size = 0;
            man_current = 0;
            menu = eMENU.MANAGE;
        }

        var top, temp1 = "", temp2 = "", temp3 = "", temp4 = "", temp5 = "";
        top = ship_current;

        draw_set_font(cjk_font(fnt_40k_14));
        draw_set_halign(fa_left);
        yy += 77;
        var main_rect;
        var repetitions = min(ship_max, ship_see);

        for (var sel = top; sel < repetitions && sel < array_length(sh_name); sel++) {
            if (sh_name[sel] != "") {
                temp1 = string(sh_name[sel]) + " (" + localize(string(sh_class[sel])) + ")";
                temp2 = string(sh_loc[sel]);
                temp3 = sh_hp[sel];
                temp4 = localize("{0} / {1} Space Used", [string(sh_cargo[sel]), string(sh_cargo_max[sel])]);

                main_rect = [
                    xx + 25,
                    yy + 64,
                    xx + 974,
                    yy + 85,
                ];

                draw_set_color(c_black);
                draw_rectangle(main_rect[0], main_rect[1], main_rect[2], main_rect[3], 0);
                draw_set_color(c_gray);
                draw_rectangle(xx + 25, yy + 64, xx + 974, yy + 85, 1);
                draw_text_transformed(xx + 27, yy + 66, temp1, 1, 1, 0);
                draw_text_transformed(xx + 27.5, yy + 66.5, temp1, 1, 1, 0);
                draw_text_transformed(xx + 364, yy + 66, string(temp2), 1, 1, 0);
                draw_text_transformed(xx + 580, yy + 66, string(temp3), 1, 1, 0);
                draw_text_transformed(xx + 730, yy + 66, string(temp4), 1, 1, 0);
                if (point_and_click(main_rect)) {
                    load_marines_into_ship(selecting_location, sel, display_unit);
                }
                yy += 20;
            }
        }

        // Load to selected
        draw_set_font(cjk_font(fnt_40k_14b));
        draw_text_transformed(xx + 320, yy + 402, localize("Click a Ship to Load Selection (Req. {0} Space)", [man_size]), 1, 1, 0);

        xx = camera_get_view_x(view_camera[0]);
        yy = camera_get_view_y(view_camera[0]);

        // draw_text_transformed(xx + 488, yy + 426, "Selection Size: " + string(man_size), 0.4, 0.4, 0);
        scr_scrollbar(974, 172, 1005, 790, 34, ship_max, ship_current);
    }
}

/// @self Asset.GMObject.obj_controller
/// @desc Draws a wrapping selection-filter group and updates sel_all on click.
/// @param {Struct.UnitButtonObject} _button The shared button used to draw each entry
/// @param {String} _all_label Label for the leading button
/// @param {String} _all_value Value sel_all takes when the leading button is pressed
/// @param {Array<String>} _entries Filter names indexed 1..8; blank entries are skipped
/// @param {Real} _left Left edge of the group
/// @param {Real} _top Top edge of the group
/// @param {Real} _right Right edge the rows must stay inside
/// @returns {Real} The Y of the first free row beneath the group
function draw_selection_filter_group(_button, _all_label, _all_value, _entries, _left, _top, _right) {
    draw_set_font(_button.font);
    _button.label = _all_label;
    _button.alpha = 1;
    _button.tooltip = "";
    _button.x1 = _left;
    _button.y1 = _top;
    if (_button.draw()) {
        sel_all = _all_value;
    }

    var _row_x = _button.x2 + _button.h_gap;
    var _row_y = _top;

    for (var i = 1; i <= 8; i++) {
        if (_entries[i] == "") {
            continue;
        }

        _button.label = string_truncate(_entries[i], 126);
        _button.alpha = 1;
        _button.x1 = _row_x;
        _button.y1 = _row_y;
        _button.update_loc(); // Measure before testing for row wrap.

        if (_button.x2 > _right) {
            _row_x = _left;
            _row_y += _button.h + _button.v_gap;
            _button.x1 = _row_x;
            _button.y1 = _row_y;
        }

        if (_button.draw()) {
            sel_all = _entries[i];
        }
        _row_x = _button.x2 + _button.h_gap;
    }

    return _row_y + _button.h + _button.v_gap;
}

/// @self Asset.GMObject.obj_controller
/// @desc Draws and handles the Manage-screen selection controls.
/// @returns {Undefined}
function draw_manage_selection_buttons() {
    var sel_loading = obj_controller.selecting_ship;
    var _unit_focus = obj_controller.unit_focus;
    var _non_control_loc = location_out_of_player_control(selecting_location);
    //draws hover over tooltips
    function gen_tooltip(tooltip_array) {
        for (var i = 0; i < array_length(tooltip_array); i++) {
            var tooltip = tooltip_array[i];
            if (scr_hit(tooltip[1][0], tooltip[1][1], tooltip[1][2], tooltip[1][3])) {
                tooltip_draw(tooltip[0]);
            }
        }
    }
    gen_tooltip(potential_tooltip);
    gen_tooltip(promotion_tooltip);
    gen_tooltip(health_tooltip);

    // Draw interaction and selection buttons
    draw_set_font(cjk_font(fnt_40k_14b));
    draw_set_color(#50a076);
    var button = new UnitButtonObject();
    //new load/unload having its own row now, calculated from Y first
    var _load_button_h = 15;
    var _load_button_h_gap = 4;
    var _load_row_total_h = (_load_button_h * 2) + _load_button_h_gap; // Load + gap + Reload

    button.h = 30;
    var action_button_bottom_y = right_ui_block.y2 - 6 - 30 - _load_row_total_h - _load_button_h_gap;
    var action_button_x = right_ui_block.x1 + 26;
    button.x1 = action_button_x;
    button.y1 = action_button_bottom_y;
    button.x2 = button.x1 + button.w;
    button.y2 = button.y1 + button.h;

    // // Re equip button
    button.label = localize("Re-equip");
    var equip_possible = !_non_control_loc && man_size > 0;
    button.alpha = equip_possible ? 1 : 0.5;
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("E"));
    button.tooltip = localize("Press Shift E");

    if (button.draw() && equip_possible) {
        set_up_equip_popup();
    }
    action_button_x += button.w + button.h_gap;

    // // Promote button
    button.x1 = action_button_x;
    button.label = localize("Promote");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("P"));
    button.tooltip = localize("Press Shift P");
    var promote_possible = sel_promoting > 0 && !_non_control_loc && man_size > 0;
    button.alpha = promote_possible ? 1 : 0.5;
    if (button.draw()) {
        if (promote_possible) {
            setup_promotion_popup();
        }
    }
    action_button_x += button.w + button.h_gap;

    // // Put in jail button
    button.x1 = action_button_x;
    button.label = localize("Jail");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("J"));
    button.tooltip = localize("Press Shift J");
    var jail_possible = man_size > 0;
    button.alpha = jail_possible ? 1 : 0.5;
    if (button.draw()) {
        if (jail_possible) {
            jail_selection();
        }
    }
    action_button_x += button.w + button.h_gap;

    // // Add bionics button
    button.x1 = action_button_x;
    button.label = localize("Add Bionics");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("B"));
    button.tooltip = localize("Press Shift B");
    var bionics_possible = man_size > 0;
    button.alpha = bionics_possible ? 1 : 0.5;
    if (button.draw()) {
        if (bionics_possible) {
            add_bionics_selection();
            if (is_struct(_unit_focus)) {
                reset_manage_unit_constants(_unit_focus);
            }
        }
    }

    var action_button_top_y = action_button_bottom_y - (button.h + button.v_gap);
    action_button_x = right_ui_block.x1 + 26;
    button.y1 = action_button_top_y;

    // // Designate as boarder unit
    button.x1 = action_button_x;
    button.label = localize("Set Boarder");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("Q"));
    button.tooltip = localize("Press Shift Q");
    var boarder_possible = sel_loading != -1 && man_size > 0;
    button.alpha = boarder_possible ? 1 : 0.5;
    if (button.draw() && boarder_possible) {
        if (boarder_possible) {
            toggle_selection_borders();
        }
    }
    action_button_x += button.w + button.h_gap;

    // // Reset changes button
    button.x1 = action_button_x;
    button.label = localize("Reset");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("R"));
    button.tooltip = localize("Press Shift R");
    var reset_possible = !_non_control_loc && man_size > 0;
    if (reset_possible) {
        button.alpha = 1;
        if (button.draw()) {
            reset_selection_equipment();
            if (is_struct(_unit_focus)) {
                reset_manage_unit_constants(_unit_focus);
            }
        }
    } else {
        button.alpha = 0.5;
        button.draw(false);
    }
    action_button_x += button.w + button.h_gap;

    // // Transfer to another company button
    button.x1 = action_button_x;
    button.label = localize("Transfer");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("T"));
    button.tooltip = localize("Press Shift T");
    var transfer_possible = !_non_control_loc && man_size > 0;
    if (transfer_possible) {
        button.alpha = 1;
        if (button.draw()) {
            set_up_transfer_popup();
        }
    } else {
        button.alpha = 0.5;
        button.draw(false);
    }
    action_button_x += button.w + button.h_gap;

    // // Move Ship button
    button.x1 = action_button_x;
    button.label = localize("Move Ship");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("M"));
    button.tooltip = localize("Press Shift M");
    var moveship_possible = !_non_control_loc && man_size > 0 && selecting_ship > -1;
    if (moveship_possible) {
        button.alpha = 1;
        if (button.draw()) {
            load_selection();
        }
    } else {
        button.alpha = 0.5;
        button.draw(false);
    }
    action_button_x += button.w + button.h_gap;

    // // Manage Tags button
    button.x1 = action_button_x;
    button.label = localize("Manage Tags");
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("F"));
    button.tooltip = localize("Press Shift F"); //Press Shift F";
    button.alpha = 1;
    if (button.draw()) {
        if (!instance_exists(obj_popup)) {
            set_up_tag_manager();
        } else if (obj_popup.type == ePOPUP_TYPE.ADD_TAGS) {
            instance_destroy(obj_popup);
        }
    }
    button.h = _load_button_h;
    button.x1 = right_ui_block.x1 + 26;
    button.y1 = action_button_bottom_y + 30 + _load_button_h_gap;
    button.x2 = button.x1 + button.w;
    button.y2 = button.y1 + button.h;
    button.label = localize("Load");
    var load_unload_possible = man_size > 0;
    button.keystroke = keyboard_check(vk_shift) && keyboard_check_pressed(ord("L"));
    button.tooltip = localize("Press Shift L");
    if (load_unload_possible) {
        button.alpha = 1;
        if (sel_loading == -1) {
            if (button.draw()) {
                load_selection();
            }
        } else if (sel_loading != -1) {
            button.label = localize("Unload");
            if (button.draw()) {
                unload_selection();
            }
        }
    } else {
        button.alpha = 0.5;
        button.draw(false);
    }

    button.move("right", true);

    button.label = localize("Reload");
    button.keystroke = false;
    if (instance_exists(obj_controller) && is_struct(_unit_focus)) {
        button.tooltip = $"{_unit_focus.last_ship.name}";
    }
    reload_possible = man_size > 0 && sel_loading == -1;
    if (reload_possible) {
        button.alpha = 1;
        if (button.draw()) {
            scr_company_load(selecting_location);
            load_marines_into_ship(selecting_location, sh_ide, display_unit, true);
        }
    } else {
        button.alpha = 0.5;
        button.draw(false);
    }

    var top_x = actions_block.x1 + 26;
    var top_y = actions_block.y1 + 70;

    var _filter_right = actions_block.x2 - 26;
    var _filter_next_y = top_y;

    if (sel_uni[1] != "") {
        // How much space the selected unit takes
        draw_set_font(cjk_font(fnt_40k_30b));
        draw_text_transformed(actions_block.x1 + 26, actions_block.y1 + 6, localize("Selection: {0} space", [man_size]), 0.5, 0.5, 0);
        // List of selected units
        draw_set_font(cjk_font(fnt_40k_14));
        draw_text_ext(actions_block.x1 + 26, actions_block.y1 + 30, selecting_dudes, -1, 550);
        // Options for the selected unit
        // draw_set_font(cjk_font(fnt_40k_30b));
        // draw_text_transformed(actions_block.x1 + 4, actions_block.x1 + 64,"Options:",0.5,0.5,0);

        // Select all units button
        // button reset code

        button.set_width = false;
        button.w = 0;
        button.h = 0;
        button.font = fnt_40k_14b;
        button.text_scale = 1;

        button.label = localize("Select All");
        button.x1 = top_x;
        button.y1 = top_y;
        button.update_loc();
        button.tooltip = "";
        button.keystroke = false;
        button.alpha = 1;
        if (button.draw()) {
            sel_all = "all";
        }

        button.x1 = top_x + button.w + button.h_gap;
        button.update_loc();
        button.label = localize("Filter Mode");
        button.alpha = filter_mode ? 1 : 0.5;
        if (button.draw()) {
            filter_mode = !filter_mode;
        }

        button.font = fnt_40k_12;

        _filter_next_y = draw_selection_filter_group(button, "All Infantry", "man", sel_uni, top_x, top_y + button.h + button.v_gap + 4, _filter_right);
    }

    if (sel_veh[1] != "") {
        button.font = fnt_40k_12;
        draw_selection_filter_group(button, "All Vehicles", "vehicle", sel_veh, top_x, _filter_next_y + 4, _filter_right);
    }
}
