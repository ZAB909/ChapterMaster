global.planet_types = [
    "Dead",
    "Ice",
    "Temperate",
    "Feudal",
    "Shrine",
    "Agri",
    "Death",
    "Hive",
    "Forge",
    "Desert",
    "Lava",
];

enum ePLAYER_BASE {
    NONE = 0,
    HOME_WORLD = 1,
    FLEET_BASED = 2,
    PENITENT = 3,
}

function find_player_spawn_star() {
    var _chosen_star = noone;
    var _allowable = false;
    var _allowables = [
        "Temperate",
        "Feudal",
        "Agri",
        "Death",
        "Ice",
        "Desert",
        "Lava",
    ];
    for (var i = 0; i < 100; i++) {
        var x_loc = irandom_range(0 + (room_width / 2), room_width - (room_width / 2));
        var y_loc = irandom_range(0 + (room_height / 2), room_height - (room_height / 2));
        if (obj_ini.homeworld_relative_loc == 0) {
            if (irandom(1)) {
                y_loc = choose(0, room_height);
                x_loc = irandom(room_width);
            } else {
                x_loc = choose(0, room_width);
                y_loc = irandom(room_height);
            }
        }
        _chosen_star = instance_nearest(x_loc, y_loc, obj_star);
        if (instance_exists(_chosen_star)) {
            for (var p = 0; p < array_length(_chosen_star.p_type); p++) {
                if (array_contains(_allowables, _chosen_star.p_type[p])) {
                    _allowable = true;
                }
            }
        }
        if (_allowable) {
            break;
        }
        instance_deactivate_object(_chosen_star);
    }
    instance_activate_all();
    return _chosen_star;
}

/// @self Id.Instance.obj_star
/// @param {Real} home_planet
function player_home_planet(home_planet) {
    var _star_names = global.name_generator.name_sets.star;
    p_type[home_planet] = obj_ini.home_type;
    planet[home_planet] = 1;
    obj_ini.home_planet = home_planet;

    if (obj_ini.home_name != "random") {
        _star_names.AddUsedName(obj_ini.home_name);
        var _old_name_star = find_star_by_name(obj_ini.home_name);
        if (_old_name_star != noone) {
            _old_name_star.name = global.name_generator.GenerateFromSet("star", false);
        }
        name = obj_ini.home_name;
    }
    array_push(p_feature[home_planet], new NewPlanetFeature(eP_FEATURES.MONASTERY));
    p_owner[home_planet] = eFACTION.PLAYER;

    p_first[home_planet] = eFACTION.PLAYER; //monestary
    if (obj_ini.homeworld_rule != 1) {
        dispo[home_planet] = -5000;
    }

    if (obj_ini.home_type == "Shrine") {
        known[eFACTION.ECCLESIARCHY] = 1;
    }
    if (obj_ini.recruiting_type == "Shrine") {
        known[eFACTION.ECCLESIARCHY] = 1;
    }

    p_lasers[home_planet] = 8;
    p_silo[home_planet] = 100;
    p_defenses[home_planet] = 75;
    if (obj_ini.custom == eCHAPTER_TYPE.PREMADE) {
        p_lasers[home_planet] = 32;
        p_silo[home_planet] = 300;
        p_defenses[home_planet] = 225;
    }

    var _planet_types = global.planet_types;
    if (p_type[home_planet] == "random") {
        p_type[home_planet] = array_random_element(_planet_types);
    }
    if (global.chapter_name != "Lamenters") {
        obj_controller.recruiting_worlds += string(name) + " I|";
    }

    p_player[home_planet] = obj_ini.man_size;

    for (var co = 0; co <= obj_ini.companies; co++) {
        for (var i = 0; i < array_length(obj_ini.TTRPG[co]); i++) {
            var unit = fetch_unit([co, i]);
            if (!is_struct(unit)) {
                continue;
            }
            if (unit.location_string == name) {
                unit.planet_location = home_planet;
            }
        }
    }
}

/// @param {Real} recruit_planet
function set_player_recruit_planet(recruit_planet) {
    var _star_names = global.name_generator.name_sets.star;
    p_type[recruit_planet] = obj_ini.recruiting_type;
    if (obj_ini.fleet_type == ePLAYER_BASE.HOME_WORLD && obj_ini.recruit_relative_loc == 2) {
        // Possibly a temporary fix, Fleet-based Chapters use Homeworld names for the Recruiting stars for some reason
        if (obj_ini.recruiting_name != "random") {
            _star_names.AddUsedName(obj_ini.recruiting_name);
            if (find_star_by_name(obj_ini.recruiting_name) != noone) {
                find_star_by_name(obj_ini.recruiting_name).name = global.name_generator.GenerateFromSet("star", false);
            }
            name = obj_ini.recruiting_name;
        }
    } else {
        if (obj_ini.home_name != "random") {
            _star_names.AddUsedName(obj_ini.home_name);
            if (find_star_by_name(obj_ini.home_name) != noone) {
                find_star_by_name(obj_ini.home_name).name = global.name_generator.GenerateFromSet("star", false);
            }
            name = obj_ini.home_name;
        }
    }
    array_push(p_feature[recruit_planet], new NewPlanetFeature(eP_FEATURES.RECRUITING_WORLD)); //recruiting world
    if (p_type[recruit_planet] == "random") {
        p_type[recruit_planet] = choose("Death", "Temperate", "Desert", "Ice", "Hive", "Fuedal");
    }
    if (global.chapter_name != "Lamenters") {
        obj_controller.recruiting_worlds += string(name) + " II|";
    }
}

/// @desc Turns the chosen star into the player's home system, expanding it to the requested
/// planet count and placing the monastery and recruiting worlds.
/// @param {Id.Instance.obj_star} chosen_star Star to convert into the home system.
/// @returns {Undefined}
function set_player_homeworld_star(chosen_star) {
    with (chosen_star) {
        if (obj_ini.recruit_relative_loc == 1 && obj_ini.home_planet_count == 0) {
            obj_ini.home_planet_count++;
        }
        planets = obj_ini.home_planet_count + 1;

        for (var _slot = 1; _slot <= planets; _slot++) {
            if (p_owner[_slot] == eFACTION.NONE) {
                p_owner[_slot] = eFACTION.IMPERIUM;
                p_first[_slot] = eFACTION.IMPERIUM;
            }
        }

        var _home_planet = irandom_range(1, planets);

        player_home_planet(_home_planet);
        var _planet_types = global.planet_types;

        if (obj_ini.recruit_relative_loc == 1) {
            var _possible_planets = [];
            for (var i = 1; i <= planets; i++) {
                if (i != _home_planet) {
                    array_push(_possible_planets, i);
                    p_type[i] = array_random_element(_planet_types);
                }
            }
            var _recruit_star = array_random_element(_possible_planets);
            set_player_recruit_planet(_recruit_star);
        } else if (obj_ini.recruit_relative_loc == 0) {
            array_push(p_feature[_home_planet], new NewPlanetFeature(eP_FEATURES.RECRUITING_WORLD)); //recruiting world
            for (var i = 1; i <= planets; i++) {
                if (i != _home_planet) {
                    p_type[i] = array_random_element(_planet_types);
                }
            }
            if (global.chapter_name != "Lamenters") {
                obj_controller.recruiting_worlds += string(name) + " II|";
            }
        } else if (obj_ini.recruit_relative_loc == 2) {
            create_recruit_system(distance_removed_star(chosen_star.x, chosen_star.y));
            for (var i = 1; i <= planets; i++) {
                if (i != _home_planet) {
                    p_type[i] = array_random_element(_planet_types);
                }
            }
        }
    }
}

function create_recruit_system(star) {
    with (star) {
        var _recruit_planet = irandom_range(1, planets);
        set_player_recruit_planet(_recruit_planet);
    }
}
