/// @self Asset.GMObject.obj_controller
function scr_destroy_gene_slave_batch(batch_id, recover_gene = true) {
    var _cur_slave = obj_ini.gene_slaves[batch_id];
    if (recover_gene) {
        obj_controller.gene_seed += _cur_slave.num;
        scr_add_item("Gene Pod Incubator", _cur_slave.num);
    }
    delete _cur_slave;
    array_delete(obj_ini.gene_slaves, batch_id, 1);
}

/// @self Asset.GMObject.obj_controller
function destroy_all_gene_slaves(recover_gene = true) {
    var _slave_length = array_length(obj_ini.gene_slaves);
    if (_slave_length > 0) {
        for (var i = _slave_length - 1; i >= 0; i--) {
            scr_destroy_gene_slave_batch(i, recover_gene);
        }
        obj_ini.gene_slaves = [];
    }
}

/// @self Asset.GMObject.obj_controller
function add_new_gene_slave() {
    if ((obj_controller.gene_seed > 0) && (obj_ini.zygote == 0)) {
        var _added = false;
        if (array_length(obj_ini.gene_slaves)) {
            var _last_set = obj_ini.gene_slaves[array_length(obj_ini.gene_slaves) - 1];
            if (_last_set.turn == obj_controller.turn) {
                _last_set.num++;
                obj_controller.gene_seed--;
                _added = true;
            }
        }
        if (!_added) {
            array_push(obj_ini.gene_slaves, {num: 1, eta: 120, harvested_once: false, turn: obj_controller.turn, assigned_apothecaries: []});
            obj_controller.gene_seed--;
        }
        scr_add_item("Gene Pod Incubator", -1);
    }
}

/// @self Asset.GMObject.obj_controller
function scr_apothecarium() {
    var xx = camera_get_view_x(view_camera[0]);
    var yy = camera_get_view_y(view_camera[0]);
    var eta = 0;
    var _apoth_screen_text = "Milord, I come with a report.  Our Chapter currently boasts " + string(obj_controller.temp[36]) + " " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + " working on a variety of things, from field-duty to research to administrative duties.  ";

    draw_sprite(spr_rock_bg, 0, xx, yy);

    draw_set_alpha(0.75);
    draw_set_color(c_black);
    draw_rectangle(xx + 342, yy + 66, xx + 903, yy + 818, 0);
    draw_set_alpha(1);
    draw_set_color(c_gray);
    draw_rectangle(xx + 342, yy + 66, xx + 903, yy + 818, 1);
    draw_line(xx + 342, yy + 426, xx + 903, yy + 426);

    draw_set_alpha(0.75);
    draw_set_color(c_black);
    draw_rectangle(xx + 945, yy + 66, xx + 1580, yy + 818, 0);
    draw_set_alpha(1);
    draw_set_color(c_gray);
    draw_rectangle(xx + 945, yy + 66, xx + 1580, yy + 818, 1);
    var _head = get_department_head(eCHAPTER_DEPARTMENTS.APOTH);
    if (is_struct(_head)) {
        if (struct_exists(obj_ini.custom_advisors, "apothecary")) {
            scr_image("advisor/splash", obj_ini.custom_advisors.apothecary, xx + 16, yy + 43, 310, 828);
        } else {
            scr_image("advisor/splash", 2, xx + 16, yy + 43, 310, 828);
        }
        draw_set_halign(fa_left);
        draw_set_color(c_gray);
        draw_set_font(fnt_40k_30b);
        draw_text_transformed(xx + 352, yy + 66, "Apothecarium", 1, 1, 0);
        draw_text_transformed(xx + 352, yy + 100, _head.name_role(), 0.6, 0.6, 0);
        draw_set_font(fnt_40k_14);
    } else if (obj_controller.menu_adept == 1) {
        scr_image("advisor/splash", 1, xx + 16, yy + 43, 310, 828);
        draw_set_halign(fa_left);
        draw_set_color(c_gray);
        draw_set_font(fnt_40k_30b);
        draw_text_transformed(xx + 352, yy + 40, "Apothecarium", 1, 1, 0);
        draw_text_transformed(xx + 352, yy + 100, $"Adept {obj_controller.adept_name}", 0.6, 0.6, 0);
        draw_set_font(fnt_40k_14);
    }

    if (obj_controller.training_apothecary == 0) {
        _apoth_screen_text += "Our Brothers are currently not assigned to train further " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + "; no more can be trained until Apothcarium funds are increased.";
    }
    if (obj_controller.training_apothecary > 0) {
        _apoth_screen_text += "Our Brothers assigned to the training of future " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + "s have taken up a ";
        if (obj_controller.training_apothecary >= 1 && obj_controller.training_apothecary <= 6) {
            var _recruit_rates = global.recruitment_rates;
            _apoth_screen_text += _recruit_rates[obj_controller.training_apothecary];
        }
        _apoth_screen_text += " pace and expect to graduate an additional " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + " in ";
        var training_points_values = global.apothecary_training_tiers;
        eta = floor((47 - obj_controller.apothecary_recruit_points) / training_points_values[obj_controller.training_apothecary]) + 1;
        _apoth_screen_text += string(eta) + " months.";
    }

    if (obj_controller.gene_seed <= 0) {
        _apoth_screen_text += "##My lord, our stocks of gene-seed are empty.  It would be best to have some come mechanicus tithe.##Further training of Neophytes is halted until our stocks replenish.";
    }
    if ((obj_controller.gene_seed > 0) && (obj_controller.gene_seed <= 10)) {
        _apoth_screen_text += "##My Brother " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + "s assigned to the gene-vault have informed me that our stocks are nearly gone.  They only number " + string(obj_controller.gene_seed) + "; this includes those recently recovered from our fallen comerades-in-arms.";
    }
    if (obj_controller.gene_seed > 10) {
        _apoth_screen_text += "##My Brother " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + "s assigned to the gene-vault have informed me that our stocks of gene-seed currently number " + string(obj_controller.gene_seed) + ".  This includes those recently recovered from our fallen comerades-in-arms.";
    }
    if (obj_controller.gene_seed > 0) {
        _apoth_screen_text += "##The stocks are stable and show no sign of mutation.";
    }

    if (obj_controller.menu_adept == 1) {
        var _recruit_pace = global.recruitment_pace_descriptions;
        _apoth_screen_text = "Your Chapter contains " + string(obj_controller.temp[36]) + " " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + ".##";
        _apoth_screen_text += "Training of further " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + "s";
        if (obj_controller.training_apothecary >= 0 && obj_controller.training_apothecary <= 6) {
            _apoth_screen_text += _recruit_pace[obj_controller.training_apothecary];
        }
        if (obj_controller.training_apothecary > 0) {
            _apoth_screen_text += "  The next " + string(obj_ini.player_role_data[eROLE.APOTHECARY].role) + " is expected in " + string(eta) + " months.";
        }
        _apoth_screen_text += "##You have " + string(obj_controller.gene_seed) + " gene-seed stocked.";
    }

    draw_text_ext(xx + 352, yy + 130, string_hash_to_newline(string(_apoth_screen_text)), -1, 536);

    var _operational_status = "";
    var _slave_length = array_length(obj_ini.gene_slaves);
    if (!obj_ini.zygote) {
        if ((obj_controller.marines + obj_controller.gene_seed <= 300) && (_slave_length == 0)) {
            _operational_status = "Our Chapter is disasterously low in number- it is strongly advised that we make use of test-slaves to breed new gene-seed.  Give me the word andwe can begin installing gestation pods.";
        } else if ((obj_controller.marines + obj_controller.gene_seed > 300) && (_slave_length == 0)) {
            _operational_status = "Our Chapter is capable of using test-slaves to breed new gene-seed.  Should our number of astartes ever plummet this may prove a valuable method of rapidly bringing our chapter back up to size.";
        } else if (_slave_length > 0) {
            _operational_status = "Our Test-Slave Incubators are working optimally.  As soon as a batch fully matures a second progenoid gland they will be harvested and prepared for use.";
        }
    }
    if (obj_ini.zygote == 1) {
        _operational_status = "Unfortunantly we cannot make use of Test-Slave Incubators.  Due to our missing Zygote any use of gestation pods is ultimately useless- no new gene-seed may be grown, no matter how long we wait.";
    }

    draw_set_halign(fa_center);
    draw_set_color(c_gray);
    draw_set_font(fnt_40k_30b);
    draw_text_transformed(xx + 622, yy + 440, "Test-Slave Incubators", 0.6, 0.6, 0);
    draw_set_halign(fa_left);
    draw_set_color(c_gray);
    draw_set_font(fnt_40k_14);
    draw_text_ext(xx + 352, yy + 477, _operational_status, -1, 536);
    var _slave_index_shown = 0;
    for (var i = 0; i < _slave_length; i++) {
        // TODO why go through all batches if we can only display 10?
        if (obj_ini.gene_slaves[i].num > 0 && _slave_index_shown < 10) {
            _slave_index_shown++;
            var _cur_slave = obj_ini.gene_slaves[i];
            draw_text(xx + 352, yy + 513 + (_slave_index_shown * 20), $"Batch {_slave_index_shown}");
            draw_text(xx + 352.5, yy + 513.5 + (_slave_index_shown * 20), $"Batch {_slave_index_shown}");
            draw_text(xx + 536, yy + 513 + (_slave_index_shown * 20), $"Eta: {_cur_slave.eta} months");
            draw_text(xx + 756, yy + 513 + (_slave_index_shown * 20), $"{_cur_slave.num} pods");
        }
    }
    draw_set_alpha(1);
    if ((obj_controller.gene_seed <= 0) || (obj_ini.zygote == 1)) {
        draw_set_alpha(0.5);
    }
    draw_set_color(c_gray);
    draw_set_color(c_black);
    if (scr_item_count("Gene Pod Incubator")) {
        if (point_and_click(draw_unit_buttons([xx + 411, yy + 793], "Add Test-Slave", [0.75, 0.75], c_green))) {
            add_new_gene_slave();
        }
    } else {
        if (scr_hit(draw_unit_buttons([xx + 411, yy + 793], "Add Test-Slave", [0.75, 0.75], c_grey))) {
            tooltip_draw("No available Gene Pod Incubators, Build more Gene Pod Incubators in the forge");
        }
    }

    draw_set_alpha(1);
    if (_slave_length <= 0) {
        draw_set_alpha(0.5);
    }
    draw_set_color(c_black);
    var _destroy_button = draw_unit_buttons([xx + 664, yy + 793], "Destroy All Incubators", [0.75, 0.75], c_red, fa_center, fnt_40k_14b, 1, true, c_gray);
    if (_slave_length > 0 && scr_hit(_destroy_button)) {
        draw_set_alpha(0.2);
        draw_set_color(c_gray);
        draw_rectangle(xx + 659, yy + 788, xx + 838, yy + 811, 0);
        if (point_and_click(_destroy_button)) {
            destroy_all_gene_slaves(true);
        }
    }
    draw_set_alpha(1);
}
